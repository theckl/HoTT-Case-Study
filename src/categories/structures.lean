import sets.algebra init2 sets.axioms sets.theories categories.basic categories.limits

universes v v' u u' w 
hott_theory

namespace hott
open hott.eq hott.sigma hott.set hott.subset hott.is_trunc hott.is_equiv hott.equiv 
     hott.categories hott.categories.limits

namespace categories

/- We define multi-sorted standard structures on categories and prove the Structure 
   Identity Principle, following and generalizing the [HoTT-Book], Section 9.8.  -/
@[hott]
structure std_structure_on (C : Type u) [category.{v} C] :=
  (S : Set)   --the set of sorts of the structure
  (P : (S -> C) -> Type w)
  (H : Œ† {s t : S -> C} (Œ± : P s) (Œ≤ : P t) (f : Œ† (x : S), s x ‚ü∂ t x), 
                                                                trunctype.{0} -1)
  (id_H : ‚àÄ {s : S -> C} (Œ± : P s), H Œ± Œ± (Œª x : S, ùüô (s x)))
  (comp_H : ‚àÄ {s t u : S -> C} (Œ± : P s) (Œ≤ : P t) (Œ≥ : P u) (f : Œ† x : S, s x ‚ü∂ t x) 
              (g : Œ† x : S, t x ‚ü∂ u x), 
              H Œ± Œ≤ f -> H Œ≤ Œ≥ g -> H Œ± Œ≥ (Œª x : S, f x ‚â´ g x))
  (std : ‚àÄ {s : S ->C} (Œ± Œ≤ : P s), 
           (H Œ± Œ≤ (Œª x : S, ùüô (s x)) √ó H Œ≤ Œ± (Œª x : S, ùüô (s x))) ‚âÉ Œ± = Œ≤)           

@[hott]
structure std_structure {C : Type u} [category.{v} C] (std_str : std_structure_on C) := 
  (carrier : std_str.S -> C)
  (str : std_str.P carrier)  

@[hott]
instance {C : Type u} [category.{v} C] (std_str : std_structure_on C) : 
  has_coe (std_structure std_str) (std_str.S -> C) :=
‚ü®Œª x : std_structure std_str, x.carrier‚ü©  

@[hott]
def std_str_eta {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  (x : std_structure std_str) : x = std_structure.mk x.carrier x.str :=
begin hinduction x, refl end  

@[hott, instance]
def std_str_is_set {C : Type u} [category.{v} C] (std_str : std_structure_on C) :
  ‚àÄ s : std_str.S -> C, is_set (std_str.P s) :=
assume s, 
have eq_eq : ‚àÄ (Œ± Œ≤ : std_str.P s), is_prop (Œ± = Œ≤), from 
  assume Œ± Œ≤, is_trunc_equiv_closed -1 (std_str.std Œ± Œ≤) (prod.is_trunc_prod _ _ -1),
is_trunc_succ_intro eq_eq 

@[hott, instance]
def std_str_po_is_prop {C : Type u} [category.{v} C] (std_str : std_structure_on C)
  {s t : std_str.S -> C} {Œ± : std_str.P s} {Œ≤ : std_str.P t} :
  ‚àÄ p : s = t, is_prop (Œ± =[p] Œ≤) :=
begin 
  intro p, hinduction p, 
  apply is_trunc_equiv_closed_rev -1 (pathover_idp _ Œ± Œ≤), 
  exact is_prop.mk (@is_set.elim _ _ Œ± Œ≤)
end   

/- Equalities like these should be produced automatically. -/
@[hott]
def ap_apd011_str {C : Type u} [category.{v} C] {std_str : std_structure_on C} 
  {s t : std_str.S -> C} {Œ± : std_str.P s} {Œ≤ : std_str.P t} : ‚àÄ (p : s = t) (q : Œ± =[p] Œ≤), 
                     ap std_structure.carrier (apd011 std_structure.mk p q) = p :=
begin intros p q, hinduction p, hinduction q, refl end 

@[hott]
def apd011_ap_str {C : Type u} [category.{v} C] {std_str : std_structure_on C} 
  {x y : std_structure std_str} : ‚àÄ p : x = y, 
  apd011 std_structure.mk (ap std_structure.carrier p)
         (pathover_ap std_str.P std_structure.carrier (apd std_structure.str p)) = 
  (std_str_eta x)‚Åª¬π ‚¨ù p ‚¨ù (std_str_eta y) :=
begin intro p, hinduction p, hinduction x, refl end  

/- As a first step, we need to construct the structure of a precategory on the standard 
   structures. -/
@[hott, instance]
def std_str_has_hom {C : Type u} [category.{v} C] (std_str : std_structure_on C) :
  has_hom (std_structure std_str) := 
has_hom.mk (Œª (str‚ÇÅ str‚ÇÇ : std_structure std_str), 
            pred_Set (Œª f : to_Set (Œ† x : std_str.S, (str‚ÇÅ.carrier x ‚ü∂ str‚ÇÇ.carrier x)), 
                       std_str.H (str‚ÇÅ.str) (str‚ÇÇ.str) f))

@[hott]
instance hom_std_C {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} : 
  has_coe ‚Ü•(str‚ÇÅ ‚ü∂ str‚ÇÇ) (Œ† x : std_str.S, (str‚ÇÅ.carrier x ‚ü∂ str‚ÇÇ.carrier x)) :=
‚ü®Œª f : str‚ÇÅ ‚ü∂ str‚ÇÇ, pred_Set_map _ f‚ü©  

@[hott]
def hom_H {C : Type u} [category.{v} C] {std_str : std_structure_on C} 
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} :
  Œ† f : str‚ÇÅ ‚ü∂ str‚ÇÇ, std_str.H str‚ÇÅ.str str‚ÇÇ.str (‚Üëf) :=
begin intro f, exact f.2 end              

@[hott]
def hom_eq_C_std {C : Type u} [category.{v} C] {std_str : std_structure_on C} 
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} (f g : str‚ÇÅ ‚ü∂ str‚ÇÇ) : 
  (f.1 = (g.1 : Œ† x : std_str.S, (str‚ÇÅ.carrier x ‚ü∂ str‚ÇÇ.carrier x))) -> (f = g) :=
assume (hom_eq_C : f.1 = g.1), 
have H_eq : f.2 =[hom_eq_C; (Œª f : to_Set (Œ† x : std_str.S, (str‚ÇÅ.carrier x ‚ü∂ str‚ÇÇ.carrier x)), 
                       std_str.H (str‚ÇÅ.str) (str‚ÇÇ.str) f)] g.2, from 
  pathover_prop_eq _ hom_eq_C (hom_H f) (hom_H g),
calc f = ‚ü®f.1, f.2‚ü© : (sigma.eta f)‚Åª¬π 
   ... = ‚ü®g.1, g.2‚ü© : dpair_eq_dpair hom_eq_C H_eq
   ... = g : sigma.eta g 

@[hott, instance]
def std_str_cat_struct {C : Type u} [category.{v} C] (std_str : std_structure_on C) :
  category_struct (std_structure std_str) :=
category_struct.mk (Œª str : std_structure std_str, 
                            ‚ü®Œª x : std_str.S, ùüô (str.carrier x), std_str.id_H str.str‚ü©) 
  (Œª (str‚ÇÅ str‚ÇÇ str‚ÇÉ : std_structure std_str) (f : str‚ÇÅ ‚ü∂ str‚ÇÇ) (g : str‚ÇÇ ‚ü∂ str‚ÇÉ), 
     ‚ü®(Œª x : std_str.S, (f : Œ† x : std_str.S, (str‚ÇÅ.carrier x ‚ü∂ str‚ÇÇ.carrier x)) x ‚â´ g.1 x), 
                        std_str.comp_H str‚ÇÅ.str str‚ÇÇ.str str‚ÇÉ.str ‚Üëf ‚Üëg (hom_H f) (hom_H g)‚ü©) 

@[hott]
def idhom_std_C {C : Type u} [category.{v} C] {std_str : std_structure_on C} 
  (str : std_structure std_str) : ‚Üë(ùüô str) = Œª x, ùüô (str.carrier x) :=
rfl  

@[hott]
def comp_hom_std_C {C : Type u} [category.{v} C] {std_str : std_structure_on C} 
  {str‚ÇÅ str‚ÇÇ str‚ÇÉ : std_structure std_str} (f : str‚ÇÅ ‚ü∂ str‚ÇÇ) (g : str‚ÇÇ ‚ü∂ str‚ÇÉ) : 
  (f ‚â´ g).1 = Œª x : std_str.S, f.1 x ‚â´ g.1 x :=
rfl  

@[hott, instance]
def std_str_precategory {C : Type u} [category.{v} C] (std_str : std_structure_on C) :
  precategory (std_structure std_str) :=
have ic : ‚àÄ (str‚ÇÅ str‚ÇÇ : std_structure std_str) (f : str‚ÇÅ ‚ü∂ str‚ÇÇ), ùüô str‚ÇÅ ‚â´ f = f, from 
begin 
  intros str‚ÇÅ str‚ÇÇ f, apply hom_eq_C_std _ _, rwr comp_hom_std_C, 
  apply eq_of_homotopy, intro x, hsimp 
end,
have ci : ‚àÄ (str‚ÇÅ str‚ÇÇ : std_structure std_str) (f : str‚ÇÅ ‚ü∂ str‚ÇÇ), f ‚â´ ùüô str‚ÇÇ = f, from 
begin 
  intros str‚ÇÅ str‚ÇÇ f, apply hom_eq_C_std _ _, rwr comp_hom_std_C, 
  apply eq_of_homotopy, intro x, hsimp 
end,
have as : ‚àÄ (str‚ÇÅ str‚ÇÇ str‚ÇÉ str‚ÇÑ: std_structure std_str) (f : str‚ÇÅ ‚ü∂ str‚ÇÇ) (g : str‚ÇÇ ‚ü∂ str‚ÇÉ) 
            (h : str‚ÇÉ ‚ü∂ str‚ÇÑ), (f ‚â´ g) ‚â´ h = f ‚â´ (g ‚â´ h), from 
begin 
  intros x y z w f g h, apply hom_eq_C_std _ _, repeat { rwr comp_hom_std_C }, 
  apply eq_of_homotopy, intro x, hsimp 
end,
precategory.mk ic ci as 

/- We prove the Structure Identity principle by splitting up the equivalence making the 
   precategory into a category into 5 equivalence and by showing that the composition of the
   5 equivalence maps is `idtoiso`. 

   The first equivalence introduces the structure components in standard structures equalities. -/
@[hott]
def std_str_comp_eq {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} :
  (str‚ÇÅ = str‚ÇÇ) ‚âÉ 
          (std_structure.mk str‚ÇÅ.carrier str‚ÇÅ.str = std_structure.mk str‚ÇÇ.carrier str‚ÇÇ.str) :=
begin hinduction str‚ÇÅ with s Œ±, hinduction str‚ÇÇ with t Œ≤, exact equiv.rfl end

/- The second equivalence is the eta principle for standard structures equalities. -/
@[hott]
def std_str_eq_eta {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {s t : std_str.S -> C} {Œ± : std_str.P s} {Œ≤ : std_str.P t} :
  (std_structure.mk s Œ± = std_structure.mk t Œ≤) ‚âÉ Œ£ (p : s = t), Œ± =[p] Œ≤ :=
let str‚ÇÅ := std_structure.mk s Œ±, str‚ÇÇ := std_structure.mk t Œ≤,
    f := Œª p : str‚ÇÅ = str‚ÇÇ, @dpair (s = t) (Œª p : s = t, Œ± =[p] Œ≤) 
         (ap std_structure.carrier p : s = t) 
         (pathover_ap std_str.P std_structure.carrier (apd std_structure.str p)),
    g := Œª pq : Œ£ (p : s = t), Œ± =[p] Œ≤, apd011 std_structure.mk pq.1 pq.2 in                         
have rinv : ‚àÄ pq : Œ£ (p : s = t), Œ± =[p] Œ≤, f (g pq) = pq, from 
  assume pq,
  have pq_1 : (f (g pq)).1 = pq.1, from 
    calc (f (g pq)).1 = ap std_structure.carrier (apd011 std_structure.mk pq.1 pq.2) : rfl
         ... = pq.1 : ap_apd011_str pq.1 pq.2,
  have pq_2 : (f (g pq)).2 =[pq_1; Œª p : s = t, Œ± =[p] Œ≤] pq.2, from po_proofs pq_1 _ _,
  calc f (g pq) = ‚ü®(f (g pq)).1, (f (g pq)).2‚ü© : sigma.eta (f (g pq))
       ... = ‚ü®pq.1, pq.2‚ü© : apd011 sigma.mk pq_1 pq_2
       ... = pq : sigma.eta pq,
have linv : ‚àÄ p : str‚ÇÅ = str‚ÇÇ, g (f p) = p, from 
  assume p,
  have qx : std_str_eta str‚ÇÅ = idp, from rfl,
  calc g (f p) = apd011 std_structure.mk (ap std_structure.carrier p) (f p).2 : rfl
       ... = (std_str_eta str‚ÇÅ)‚Åª¬π ‚¨ù p ‚¨ù (std_str_eta str‚ÇÇ) : apd011_ap_str p
       ... = p : by rwr qx; rwr idp_inv; rwr idp_con p; rwr con_idp p,
equiv.mk f (adjointify f g rinv linv)    

/- The third equivalence exchanges equalities and isomorphisms. -/
@[hott]
def strpair_id_to_iso {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {s t : std_str.S -> C} {Œ± : std_str.P s} {Œ≤ : std_str.P t} :
  (Œ£ (p : s = t), Œ± =[p] Œ≤) ‚âÉ (Œ£ (f : Œ† x, s x ‚âÖ t x), std_str.H Œ± Œ≤ (Œª x, (f x).hom) and 
                                                        std_str.H Œ≤ Œ± (Œª x, (f x).inv)) :=
begin                                                        
  let str‚ÇÅ := std_structure.mk s Œ±, let str‚ÇÇ := std_structure.mk t Œ≤, 
  have po_hom : Œ† (p : s = t) (q : Œ± =[p] Œ≤), std_str.H Œ± Œ≤ (Œª x, (idtoiso (ap10 p x)).hom) and 
                                              std_str.H Œ≤ Œ± (Œª x, (idtoiso (ap10 p x)).inv), from 
  begin 
    intros p q, hinduction p, 
    have q' : Œ± = Œ≤, from eq_of_pathover_idp q, 
    have r : (Œª x : std_str.S, (idtoiso (ap10 (@idp _ s) x)).hom) = 
              Œª x : std_str.S, ùüô (s x), from rfl,
    have r' : (Œª x : std_str.S, (idtoiso (ap10 (@idp _ s) x)).inv) = 
               Œª x : std_str.S, ùüô (s x), from rfl,         
    rwr r, rwr r', rwr <- q', exact (std_str.id_H Œ±, std_str.id_H Œ±) 
  end,
  have idtoiso_hom_po : ‚àÄ p : s = t, std_str.H Œ± Œ≤ (Œª x, (idtoiso (ap10 p x)).hom) and 
                            std_str.H Œ≤ Œ± (Œª x, (idtoiso (ap10 p x)).inv) -> Œ± =[p] Œ≤, from
    begin intros p H, hinduction p, apply pathover_idp_of_eq, exact std_str.std Œ± Œ≤ H end,
  have hom_po : Œ† (f : Œ† x, s x ‚âÖ t x), (std_str.H Œ± Œ≤ (Œª x, (f x).hom) and 
                  std_str.H Œ≤ Œ± (Œª x, (f x).inv)) -> 
                  Œ± =[eq_of_homotopy (Œª x, category.isotoid (f x))] Œ≤, from 
    assume f H,
    have r : f = Œª x, idtoiso (ap10 (eq_of_homotopy (Œª x, category.isotoid (f x))) x), from
      begin 
        apply eq_of_homotopy, intro x, change f x = idtoiso (apd10 _ x),
        rwr apd10_eq_of_homotopy, change _ = idtoiso (idtoiso‚Åª¬π·∂† (f x)), 
        rwr category.idtoiso_rinv (f x) 
      end, 
    begin 
      apply idtoiso_hom_po, 
      rwr <- eq_of_homotopy (Œª x, ap iso.hom (apd10 r x)), 
      rwr <- eq_of_homotopy (Œª x, ap iso.inv (apd10 r x)), exact H 
    end, 
  let F := Œª (pq : Œ£ (p : s = t), Œ± =[p] Œ≤), @dpair (Œ† x, s x ‚âÖ t x) 
    (Œª f : Œ† x, s x ‚âÖ t x, std_str.H Œ± Œ≤ (Œª x, (f x).hom) and std_str.H Œ≤ Œ± (Œª x, (f x).inv)) 
    (Œª x, idtoiso (ap10 pq.1 x)) (po_hom pq.1 pq.2),  
  let G := Œª iso_H : (Œ£ (f : Œ† x, s x ‚âÖ t x), std_str.H Œ± Œ≤ (Œª x, (f x).hom) and 
                       std_str.H Œ≤ Œ± (Œª x, (f x).inv)), @dpair (s = t) (Œª p : s = t, Œ± =[p] Œ≤) 
                (eq_of_homotopy (Œª x, category.isotoid (iso_H.1 x))) (hom_po iso_H.1 iso_H.2),
  have rinv : ‚àÄ iso_H, F (G iso_H) = iso_H, from 
  begin
    intro iso_H, fapply sigma_eq,
    { apply eq_of_homotopy, intro x, 
      change idtoiso (apd10 _ x) = iso_H.1 x, rwr apd10_eq_of_homotopy,
      change idtoiso (idtoiso‚Åª¬π·∂† (iso_H.1 x)) = iso_H.1 x,
      exact category.idtoiso_rinv (iso_H.1 x) },
    { apply pathover_of_eq_tr, exact is_prop.elim _ _ }
  end,
  have linv : ‚àÄ (pq : Œ£ (p : s = t), Œ± =[p] Œ≤), G (F pq) = pq, from 
  begin
    intro pq, hinduction pq.1 with h,
    fapply sigma_eq,
    { change (eq_of_homotopy (Œª x, idtoiso‚Åª¬π·∂† (idtoiso (apd10 pq.1 x)))) = _,  
      have r : Œ† x, apd10 pq.1 x = @idp _ (s x), from begin intro x, rwr h end, 
      have q : (Œª x, idtoiso‚Åª¬π·∂† (idtoiso (apd10 pq.1 x))) = Œª x, (@idp _ (s x)), from 
      begin 
        apply eq_of_homotopy, intro x, change idtoiso‚Åª¬π·∂† (idtoiso (apd10 pq.1 x)) = _,
        rwr r x, rwr category.idtoiso_linv ((@idp _ (s x)))
      end,  
      rwr q, rwr h, rwr eq_of_homotopy_idp }, 
    { apply pathover_of_eq_tr, exact is_prop.elim _ _ }, 
  end,                                                             
  exact equiv.mk F (adjointify F G rinv linv) 
end

/- The fourth equivalence splits up equalities of standard structure isomorphisms. -/
@[hott]
def iso_std_C {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} (F : str‚ÇÅ ‚âÖ str‚ÇÇ) : Œ† x, str‚ÇÅ.carrier x ‚âÖ str‚ÇÇ.carrier x :=
let f := (F.hom : str‚ÇÅ ‚ü∂ str‚ÇÇ).1, g := F.inv.1 in
have rinv : Œ† x, (g x) ‚â´ (f x) = ùüô (str‚ÇÇ.carrier x), 
  begin intro x, change (F.inv ‚â´ F.hom).1 x = _, rwr F.r_inv end,
have linv : Œ† x, (f x) ‚â´ (g x) = ùüô (str‚ÇÅ.carrier x), 
  begin intro x, change (F.hom ‚â´ F.inv).1 x = _, rwr F.l_inv end, 
assume x, iso.mk (f x) (g x) (rinv x) (linv x)  

@[hott]
def str_iso_eq_comp {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {s t : std_str.S -> C} {Œ± : std_str.P s} {Œ≤ : std_str.P t} :
  (Œ£ (f : Œ† x, s x ‚âÖ t x), std_str.H Œ± Œ≤ (Œª x, (f x).hom) and std_str.H Œ≤ Œ± (Œª x, (f x).inv)) ‚âÉ 
  (std_structure.mk s Œ± ‚âÖ std_structure.mk t Œ≤) :=
begin
  let str‚ÇÅ := std_structure.mk s Œ±, let str‚ÇÇ := std_structure.mk t Œ≤,
  fapply equiv.mk,
  /- We define `F : (Œ£ (f : Œ† x, s x ‚âÖ t x), std_str.H Œ± Œ≤ (Œª x, (f x).hom) and`
                                            `std_str.H Œ≤ Œ± (Œª x, (f x).inv)) -> (str‚ÇÅ ‚âÖ str‚ÇÇ)`. -/
  { intro iso_H, 
    fapply iso.mk,
    { exact ‚ü®Œª x, (iso_H.1 x).hom, iso_H.2.1‚ü© },
    { exact ‚ü®Œª x, (iso_H.1 x).inv, iso_H.2.2‚ü© },
    { apply hom_eq_C_std _ _, repeat { rwr comp_hom_std_C }, hsimp, 
      apply eq_of_homotopy, intro x, hsimp, rwr (iso_H.1 x).r_inv },
    { apply hom_eq_C_std _ _, repeat { rwr comp_hom_std_C }, hsimp, 
      apply eq_of_homotopy, intro x, hsimp, rwr (iso_H.1 x).l_inv } },
  { fapply adjointify,
    /- Now we define `G : (str‚ÇÅ ‚âÖ str‚ÇÇ) -> (Œ£ (f : Œ† x, s x ‚âÖ t x), std_str.H Œ± Œ≤ (Œª x, (f x).hom) and`
                                            `std_str.H Œ≤ Œ± (Œª x, (f x).inv))`. -/
    { intro f, 
      fapply sigma.mk,
      { exact iso_std_C f },
      { exact (f.hom.2, f.inv.2) } },
    /- `r_inv : ‚àÄ f : str‚ÇÅ ‚âÖ str‚ÇÇ, F (G f) = f` -/  
    { intro f,
      apply hom_eq_to_iso_eq, apply hom_eq_C_std _ _, 
      hsimp, refl },
    /- `l_inv : ‚àÄ iso_H : (Œ£ (f : Œ† x, s x ‚âÖ t x), std_str.H Œ± Œ≤ (Œª x, (f x).hom) and`
                                            `std_str.H Œ≤ Œ± (Œª x, (f x).inv)), G (F iso_H)) = iso_H` -/  
    { intro iso_H, 
      fapply sigma_eq, 
      { apply eq_of_homotopy, intro x, apply hom_eq_to_iso_eq, refl },
      { apply pathover_of_eq_tr, exact is_prop.elim _ _ } } }
end    

/- The last equivalence introduces the structure components in standard structures isomorphies. -/
@[hott]
def std_str_comp_iso {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} :
  (str‚ÇÅ ‚âÖ str‚ÇÇ) ‚âÉ (std_structure.mk str‚ÇÅ.carrier str‚ÇÅ.str ‚âÖ std_structure.mk str‚ÇÇ.carrier str‚ÇÇ.str) :=
begin hinduction str‚ÇÅ with s Œ±, hinduction str‚ÇÇ with t Œ≤, exact equiv.rfl end

/- Finally, we show that the composition of the five equivalences is `idtoiso`. -/
@[hott]
def comp_eqv_idtoiso {C : Type u} [category.{v} C] {std_str : std_structure_on C}
  {str‚ÇÅ str‚ÇÇ : std_structure std_str} (p : str‚ÇÅ = str‚ÇÇ) :
        (std_str_comp_iso.to_fun‚Åª¬π·∂† (str_iso_eq_comp.to_fun (strpair_id_to_iso.to_fun 
                            (std_str_eq_eta.to_fun (std_str_comp_eq.to_fun p))))) = idtoiso p :=                            
begin
  hinduction p, hinduction str‚ÇÅ with s Œ±,
  let str := std_structure.mk s Œ±,
  have p‚ÇÅ : std_str_comp_eq.to_fun (refl str) = refl str, from rfl,
  have p‚ÇÇ : std_str_eq_eta.to_fun (refl str) = ‚ü®refl s, idpo‚ü©, from rfl,
  have p‚ÇÉ : strpair_id_to_iso.to_fun ‚ü®refl s, idpo‚ü© = 
            ‚ü®Œª x, id_is_iso (s x), (std_str.id_H Œ±, std_str.id_H Œ±)‚ü©, from rfl,
  have p‚ÇÑ : str_iso_eq_comp.to_fun ‚ü®Œª x, id_is_iso (s x), (std_str.id_H Œ±, std_str.id_H Œ±)‚ü© = id_is_iso str,
    by apply hom_eq_to_iso_eq; refl,         
  rwr idtoiso_refl_eq, rwr p‚ÇÅ, rwr p‚ÇÇ, rwr p‚ÇÉ, rwr p‚ÇÑ
end     

/- Now we can prove the equivalence and thus the Structure Identity Principle. -/
@[hott]
def std_str_eq_eqv_iso {C : Type u} [category.{v} C] {std_str : std_structure_on C} :
  ‚àÄ x y : std_structure std_str, (x = y) ‚âÉ (x ‚âÖ y) :=
assume x y, std_str_comp_eq ‚¨ùe std_str_eq_eta ‚¨ùe strpair_id_to_iso ‚¨ùe 
            str_iso_eq_comp ‚¨ùe std_str_comp_iso‚Åª¬π·µâ 

@[hott, instance]
def structure_identity_principle {C : Type u} [category.{v} C] (std_str : std_structure_on C) :
  category (std_structure std_str) :=
have idtoiso_eq : ‚àÄ x y : std_structure std_str, (std_str_eq_eqv_iso x y).to_fun = @idtoiso _ _ x y, from
  begin 
    intros x y, apply eq_of_homotopy, 
    intro p, exact comp_eqv_idtoiso p 
  end,      
have idtoiso_eqv : ‚àÄ x y : std_structure std_str, is_equiv (@idtoiso _ _ x y), from 
  assume x y,
  have eqv : is_equiv (std_str_eq_eqv_iso x y).to_fun, from (std_str_eq_eqv_iso x y).to_is_equiv,
  by rwr idtoiso_eq x y at eqv; exact eqv, 
category.mk idtoiso_eqv 

/- The forgetful functor from a category of multi-sorted standard structures to the underlying 
   product category, with one object of the underlying category as a factor for each sort. 
   
   Even if the underlying category of the standard structure has products, the forgetful 
   functor should not map to the product of the objects corresponding to the sorts: The 
   forgetful functor may not be faithful, as it's not clear how to get back the component 
   morphisms from the morphisms between products - this only works if projections are epic, for
   example if the category has a zero = initial + terminal objects. But that's not the case 
   already for sets.

   The product category is implemented as the functor category from the discrete set of sorts 
   to the underlying category. -/
@[hott]
def forget_str {C : Type u} [category.{v} C] (std_str : std_structure_on C) :
  (std_structure std_str) ‚•§ ((discrete std_str.S) ‚•§ C) :=
begin
  fapply functor.mk,  
  { intro str, exact discrete.functor str.carrier }, -- map of objects
  { intros str‚ÇÅ str‚ÇÇ f, exact discrete.nat_trans (Œª s, f.1 s) }, -- map of morphisms
  { intro str‚ÇÅ, apply nat_trans_eq, refl },  -- preserves identity morphisms
  { intros str‚ÇÅ str‚ÇÇ str‚ÇÉ f g, apply nat_trans_eq, refl }  
                                                     -- preserves compositions of morphisms 
end 

/- The forgetful functor is faithful. -/
@[hott]
def forget_is_faithful {C : Type u} [category.{v} C] (std_str : std_structure_on C) : 
  is_faithful_functor _ _ (forget_str std_str) :=
begin 
  intros str‚ÇÅ str‚ÇÇ f‚ÇÅ f‚ÇÇ p, fapply sigma.sigma_eq, 
  { apply eq_of_homotopy, intro s, exact apd10 (ap nat_trans.app p) s },
  { apply pathover_of_tr_eq, exact is_prop.elim _ _ } 
end  

/- We want to construct limits of diagrams of standard structures fom the limits of the associated 
   diagrams in the underlying ategory indexed by the sorts. In principle these diagrams can be 
   retrieved from the forgetful functor, but it is more effective to construct them directly: 
   A `J`-diagram of standard structures yields a tupel of `J`-diagrams in the underlying category, 
   indexed by the sorts. -/
@[hott]
def forget_diagram {J : Type.{u'}} [precategory.{v'} J] {C : Type u} [category.{v} C] 
  {std_str : std_structure_on C} (F : J ‚•§ std_structure std_str) : Œ† x : std_str.S, J ‚•§ C :=
begin
  intro x,
  fapply functor.mk, 
  { intro j, exact (F.obj j).carrier x },
  { intros j k f, exact (F.map f).1 x },
  { intro j, change (F.map (ùüô j)).1 x = ùüô ((F.obj j).carrier x), rwr functor.map_id },
  { intros j k l f g, change (F.map (f ‚â´ g)).1 x  = ((F.map f).1 x) ‚â´ ((F.map g).1 x), 
    rwr functor.map_comp }
end  

/- Next, we extract cones in the underlying category indexed by the sorts from a cone of standard structures. -/
@[hott, reducible]
def str_cone_to_sort_cones {J : Set.{u'}} [precategory.{v'} J] {C : Type u} [category.{v} C] 
  {std_str : std_structure_on C} {F : J ‚•§ (std_structure std_str)} (s : cone F) : 
  Œ† x : std_str.S, cone (forget_diagram F x) :=
begin 
  intro x, fapply cone.mk, 
  { exact s.X.carrier x },  -- vertex
  { fapply nat_trans.mk,
    { intro j, exact (s.œÄ.app j).1 x },  --transformation of objects
    { intros j k f, hsimp, change (s.œÄ.app k).1 x = (Œª x, (s.œÄ.app j).1 x ‚â´ (F.map f).1 x) x, 
      rwr <- apd10 (comp_hom_std_C _ _) x, rwr <- ap sigma.fst (s.œÄ.naturality f), 
      rwr precategory.id_comp } }  --naturality
end 

/- We can construct the limit cone of a diagram of standard structures from the limit cones of the diagrams of the
   underlying category associated to the sorts if (and only if, but we won' proof that)
   - the tupel of vertices of the limit cones carries a structure,
   - the tupel of leg morphisms of the limit cones associated to each diagram arrow respects the structures,
   - the tupel of lifting morphisms from the vertices of cones associated to each sort to the vertices of the limit 
     cones respect the structures. 
   
   To show this we collect the data of this criterion in a type: -/
@[hott]
structure limit_cone_str_data {J : Set.{u'}} [precategory.{v'} J] {C : Type u} 
  [category.{v} C] {std_str : std_structure_on C} {F : J ‚•§ (std_structure std_str)} 
  (lc : Œ† x : std_str.S, limit_cone (forget_diagram F x)) :=
(lc_str : std_str.P (Œª x, (lc x).cone.X)) 
(lc_legs_H : Œ† (j : J), std_str.H lc_str ((F.obj j).str) (Œª x, (lc x).cone.œÄ.app j))
(lift_H : Œ† (s : cone F), std_str.H s.X.str lc_str (Œª x, (lc x).is_limit.lift (str_cone_to_sort_cones s x)))

@[hott]
def str_limit_cone {J : Set.{u'}} [precategory.{v'} J] {C : Type u} [category.{v} C] 
  {std_str : std_structure_on C} {F : J ‚•§ (std_structure.{v u w} std_str)} 
  (lc : Œ† x : std_str.S, limit_cone (forget_diagram F x)) (lcd : limit_cone_str_data lc) : limit_cone F :=
begin 
  fapply limit_cone.mk, 
  { fapply cone.mk, -- the limit cone 
    { exact std_structure.mk (Œª x, (lc x).cone.X) lcd.lc_str },
    { fapply nat_trans.mk, 
      { intro j, 
        exact ‚ü®Œª x, (lc x).cone.œÄ.app j, lcd.lc_legs_H j‚ü© },
      { intros j k f, hsimp, 
        apply @hom_eq_C_std _ _ _ (std_structure.mk (Œª x, (lc x).cone.X) lcd.lc_str) (F.obj k), 
        rwr comp_hom_std_C, hsimp, apply eq_of_homotopy, intro x, hsimp, 
        have H : (lc x).cone.œÄ.app k = (ùüô (lc x).cone.X) ‚â´ (lc x).cone.œÄ.app k, by rwr precategory.id_comp,
        rwr H, exact (lc x).cone.œÄ.naturality f } } },
  { fapply is_limit.mk, -- the limit cone is a limit
    { intro s, 
      exact ‚ü®Œª x, (lc x).is_limit.lift (str_cone_to_sort_cones s x), lcd.lift_H s‚ü© },
    { intros s j, apply hom_eq_C_std, rwr comp_hom_std_C, hsimp, apply eq_of_homotopy, intro x,
      change (lc x).is_limit.lift (str_cone_to_sort_cones s x) ‚â´ (lc x).cone.œÄ.app j = (s.œÄ.app j).fst x,
      exact (lc x).is_limit.fac (str_cone_to_sort_cones s x) j },
    { intros s m fac_m, apply hom_eq_C_std, hsimp, apply eq_of_homotopy, intro x, 
      have fac_m1 : ‚àÄ j : J, m.1 x ‚â´ (lc x).cone.œÄ.app j = (str_cone_to_sort_cones s x).œÄ.app j, from 
        begin intro j, change _ = (s.œÄ.app j).1 x, rwr <- apd10 (ap sigma.fst (fac_m j)) x end,
      exact (lc x).is_limit.uniq (str_cone_to_sort_cones s x) (m.1 x) fac_m1 } }
end                

@[hott]
def str_has_limit {J : Set.{u'}} [precategory.{v'} J] {C : Type u} [category.{v} C] 
  [has_limits_of_shape J C] {std_str : std_structure_on C} (F : J ‚•§ (std_structure std_str)) 
  (lcd : limit_cone_str_data (Œª x, get_limit_cone (forget_diagram F x))) : has_limit F :=
has_limit.mk (str_limit_cone (Œª x, get_limit_cone (forget_diagram F x)) lcd)                                           

@[hott, instance]
def std_structure_has_limits_of_shape {J : Set.{u'}} [precategory.{v'} J] {C : Type u} 
  [category.{v} C] [has_limits_of_shape J C] {std_str : std_structure_on C} 
  (lcd_F : Œ† F : J ‚•§ (std_structure std_str), limit_cone_str_data 
                                                (Œª x, get_limit_cone (forget_diagram  F x))) : 
  has_limits_of_shape J (std_structure std_str) :=
has_limits_of_shape.mk (Œª F, str_has_limit F (lcd_F F))


open signature

/- We define structures of a given signature over a category `C` with products. -/
@[hott]  
structure Sig_structure_on {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] (car : sign.sorts -> C) :=  
( ops : ‚àÄ o : sign.ops, ‚àè (Œª a : sign.ops_arity o, car (sign.ops_source o a)) ‚ü∂ 
                                                                        car (sign.ops_target o) )
( rels : ‚àÄ r : sign.rels, subobject (‚àè (Œª a : sign.rels_arity r, car (sign.rels_comp a))) )

@[hott]
def Sig_str_eq {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] {car : sign.sorts -> C} {S T : Sig_structure_on car} :
  (S.ops = T.ops) -> (S.rels = T.rels) -> S = T :=
begin 
  hinduction S with ops‚ÇÅ rels‚ÇÅ, hinduction T with ops‚ÇÇ rels‚ÇÇ, hsimp, 
  intros ops_eq rels_eq, exact ap011 Sig_structure_on.mk ops_eq rels_eq 
end

@[hott]
def Sig_str_eq_eta {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] {car : sign.sorts -> C} {S T : Sig_structure_on car} (p : S = T) :
  Sig_str_eq (ap Sig_structure_on.ops p) (ap Sig_structure_on.rels p) = p :=
begin hinduction p, hinduction S, refl end  

@[hott, instance]
def is_set_Sig_str_on {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] (car : sign.sorts -> C) : is_set (Sig_structure_on car) :=
begin
  fapply is_set.mk, intros x y p q, 
  rwr <- Sig_str_eq_eta p, rwr <- Sig_str_eq_eta q,
  apply ap011 Sig_str_eq, apply is_set.elim, apply is_set.elim
end   

@[hott]
structure is_Sig_structure_hom {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] {car‚ÇÅ car‚ÇÇ : sign.sorts -> C} (S‚ÇÅ : Sig_structure_on car‚ÇÅ)
  (S‚ÇÇ : Sig_structure_on car‚ÇÇ) (f : Œ† x : sign.sorts, car‚ÇÅ x ‚ü∂ car‚ÇÇ x) := 
( ops_pres : Œ† o : sign.ops, S‚ÇÅ.ops o ‚â´ f (sign.ops_target o) = 
                                               (‚àèh (Œª a, f (sign.ops_source o a))) ‚â´ S‚ÇÇ.ops o )
( rels_pres : Œ† r : sign.rels, Œ£ h : (S‚ÇÅ.rels r).obj ‚ü∂ (S‚ÇÇ.rels r).obj, 
    (S‚ÇÅ.rels r).hom ‚â´ (‚àèh (Œª a, f (sign.rels_comp a))) = h ‚â´ (S‚ÇÇ.rels r).hom )

@[hott, instance]
def is_prop_is_Sig_structure_hom {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] {car‚ÇÅ car‚ÇÇ : sign.sorts -> C} {S‚ÇÅ : Sig_structure_on car‚ÇÅ}
  {S‚ÇÇ : Sig_structure_on car‚ÇÇ} (f : Œ† x : sign.sorts, car‚ÇÅ x ‚ü∂ car‚ÇÇ x) : 
  is_prop (is_Sig_structure_hom S‚ÇÅ S‚ÇÇ f) :=
begin
  fapply is_prop.mk, intros h‚ÇÅ h‚ÇÇ, hinduction h‚ÇÅ with op‚ÇÅ rp‚ÇÅ, hinduction h‚ÇÇ with op‚ÇÇ rp‚ÇÇ,
  fapply ap011 is_Sig_structure_hom.mk, 
  { apply eq_of_homotopy, intro o, exact is_prop.elim _ _ },
  { apply eq_of_homotopy, intro r, fapply sigma.sigma_eq, 
    { exact (S‚ÇÇ.rels r).is_mono _ (rp‚ÇÅ r).1 (rp‚ÇÇ r).1 ((rp‚ÇÅ r).2‚Åª¬π‚¨ù(rp‚ÇÇ r).2) },
    { apply pathover_of_tr_eq, exact is_prop.elim _ _ } }
end                                                         

@[hott]
def id_is_Sig_str_hom {sign : fo_signature} {C : Type u} [category.{v} C] 
  [has_products.{v u 0} C] {car : sign.sorts -> C} (S : Sig_structure_on car) :
  is_Sig_structure_hom S S (Œª x, ùüô (car x)) :=
begin 
  fapply is_Sig_structure_hom.mk, 
  { intro o, rwr pi_hom_id, hsimp }, 
  { intro r, fapply dpair, exact ùüô (S.rels r).obj, rwr pi_hom_id, hsimp } 
end  

@[hott]
def std_str_of_Sig_str (sign : fo_signature) (C : Type u) [category.{v} C] 
  [has_products.{v u 0} C] : std_structure_on C :=
begin
  fapply std_structure_on.mk,
  { exact sign.sorts }, --sorts
  { exact Œª car : sign.sorts -> C, Sig_structure_on car }, --structure
  { intros x y S T h, 
    exact prop_resize (to_Prop (is_Sig_structure_hom S T h)) }, --homomorphisms
  { intros car S, apply prop_to_prop_resize, fapply is_Sig_structure_hom.mk, 
    { intro o, hsimp, rwr pi_hom_id, hsimp },
    { intro r, rwr pi_hom_id, fapply dpair, exact ùüô (S.rels r).obj, hsimp } }, --identity
  { intros car‚ÇÅ car‚ÇÇ car‚ÇÉ S T U f g is_hom_f is_hom_g, apply prop_to_prop_resize, 
    fapply is_Sig_structure_hom.mk, 
    { intro o, rwr <- precategory.assoc, 
      rwr (prop_resize_to_prop is_hom_f).ops_pres o, rwr precategory.assoc,
      rwr (prop_resize_to_prop is_hom_g).ops_pres o, rwr <- precategory.assoc,
      rwr pi_hom_comp },
    { intros r, 
      let h‚ÇÅ := ((prop_resize_to_prop is_hom_f).rels_pres r).1,
      let p‚ÇÅ := ((prop_resize_to_prop is_hom_f).rels_pres r).2,
      let h‚ÇÇ := ((prop_resize_to_prop is_hom_g).rels_pres r).1,
      let p‚ÇÇ := ((prop_resize_to_prop is_hom_g).rels_pres r).2,
      fapply dpair,
      { exact h‚ÇÅ ‚â´ h‚ÇÇ },
      { rwr <- pi_hom_comp, rwr <- precategory.assoc, rwr p‚ÇÅ, rwr precategory.assoc, rwr p‚ÇÇ, 
        rwr <- precategory.assoc } } }, --composition
  { intros car S T, fapply equiv.mk, 
    { intro Sig_homs, 
      hinduction S with ops‚ÇÅ rels‚ÇÅ, hinduction T with ops‚ÇÇ rels‚ÇÇ, 
      fapply ap011 Sig_structure_on.mk, 
      { apply eq_of_homotopy, intro o, 
        rwr <- precategory.id_comp (ops‚ÇÇ o), rwr <- precategory.comp_id (ops‚ÇÅ o),
        rwr <- pi_hom_id, exact (prop_resize_to_prop Sig_homs.1).ops_pres o },
      { apply eq_of_homotopy, intro r, apply (iso_mono_to_equal_subobj _ _),
        apply (homs_eqv_iso_of_monos _ _).to_fun, fapply pair,
        { fapply hom_of_monos.mk, 
          { exact ((prop_resize_to_prop Sig_homs.1).rels_pres r).1 }, 
          { let p‚ÇÅ := ((prop_resize_to_prop Sig_homs.1).rels_pres r).2, rwr <- p‚ÇÅ, rwr pi_hom_id,
            hsimp } }, 
        { fapply hom_of_monos.mk, 
          { exact ((prop_resize_to_prop Sig_homs.2).rels_pres r).1 }, 
          { let p‚ÇÇ := ((prop_resize_to_prop Sig_homs.2).rels_pres r).2, rwr <- p‚ÇÇ, rwr pi_hom_id,
            hsimp } } } },
    { fapply adjointify, 
      { intro Sig_str_eq, rwr Sig_str_eq, apply pair,
        exact prop_to_prop_resize (id_is_Sig_str_hom T), 
        exact prop_to_prop_resize (id_is_Sig_str_hom T) },
      { intro b, exact is_set.elim _ _ },
      { intro a, exact is_prop.elim _ _ } } } --standard structure
end  

@[hott, reducible]
def Sig_structure (sign : fo_signature) (C : Type u) [category.{v} C] [has_products.{v u 0} C] :=
  std_structure (std_str_of_Sig_str sign C)

@[hott, instance]
def Sig_str_precategory (sign : fo_signature) (C : Type u) [category.{v} C] [has_products.{v u 0} C] : 
  precategory (Sig_structure sign C) := 
std_str_precategory (std_str_of_Sig_str sign C)

@[hott, instance]
def Sig_str_category (sign : fo_signature) (C : Type u) [category.{v} C] [has_products.{v u 0} C] : 
  category (Sig_structure sign C) := 
structure_identity_principle (std_str_of_Sig_str sign C)


end categories

end hott