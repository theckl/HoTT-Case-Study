import sets.algebra init2 types2 sets.axioms

universes v v' v'' u u' u'' w 
hott_theory

namespace hott
open hott.eq hott.sigma hott.set hott.subset hott.is_trunc hott.is_equiv

/-
We introduce precategories and categories following the HoTT book, 
Sec. 9.1. HoTT precategories have sets of homomorphisms, and HoTT categories 
prescribe univalence : Isomorphisms are equivalent to identities of objects.

As far as possible we copy the mathlib-code in [category_theory.category.default]. In particular,
we keep the distinction of universe levels for objects and morphisms of a category.
-/

namespace categories

/-- A 'notation typeclass' on the way to defining a precategory. -/
@[hott]
class has_hom (obj : Type u) : Type (max u (v+1)) :=
  (hom : obj â†’ obj â†’ Set.{v})

infixr ` âŸ¶ `:10 := has_hom.hom  -- type as \h

/-- A preliminary structure on the way to defining a precategory,
containing the data, but none of the axioms. -/
@[hott]
class category_struct (obj : Type u) 
extends has_hom.{v} obj : Type (max u (v+1)) :=
(id       : Î  a : obj, hom a a)
(comp     : Î  {a b c : obj}, (a âŸ¶ b) â†’ (b âŸ¶ c) â†’ (a âŸ¶ c))

notation `ğŸ™` := category_struct.id -- type as \b1
infixr ` â‰« `:80 := category_struct.comp -- type as \gg

/-- The structure of a precategory. -/
@[hott, class]
structure precategory (obj : Type u) 
extends category_struct.{v} obj : Type (max u (v+1)) :=
(id_comp : âˆ€ {a b : obj} (f : hom a b), ğŸ™ a â‰« f = f)
(comp_id : âˆ€ {a b : obj} (f : hom a b), f â‰« ğŸ™ b = f)
(assoc   : âˆ€ {a b c d : obj} (f : hom a b) (g : hom b c) (h : hom c d),
  (f â‰« g) â‰« h = f â‰« (g â‰« h))

attribute [hsimp] precategory.id_comp precategory.comp_id precategory.assoc

/- Defintion of categorical isomorphisms. -/
@[hott]
structure iso {C : Type u} [precategory.{v} C] (a b : C) :=
  (hom : a âŸ¶ b)
  (inv : b âŸ¶ a) 
  (r_inv : inv â‰« hom = ğŸ™ b) 
  (l_inv : hom â‰« inv = ğŸ™ a) 

postfix `â»Â¹Ê°`:std.prec.max_plus := iso.inv

infix ` â‰… `:25 := iso

@[hott]
structure is_iso {C : Type u} [precategory.{v} C] {a b : C} (f : a âŸ¶ b) :=
  (inv : b âŸ¶ a)
  (r_inv : inv â‰« f = ğŸ™ b)
  (l_inv : f â‰« inv = ğŸ™ a)

@[hott]
def is_iso_to_iso {C : Type u} [precategory.{v} C] {a b : C} (f : a âŸ¶ b) 
  (H : is_iso f) : a â‰… b := iso.mk f H.inv H.r_inv H.l_inv

@[hott]
def iso_to_is_iso {C : Type u} [precategory.{v} C] {a b : C} (f : a â‰… b) : 
  is_iso f.hom := is_iso.mk f.inv f.r_inv f.l_inv  

@[hott]
def iso.eta {C : Type u} [precategory.{v} C] {a b : C} (i : a â‰… b) : 
  i = iso.mk i.hom i.inv i.r_inv i.l_inv :=
begin hinduction i, hsimp end  

@[hott, hsimp]
def inv_iso {C : Type u} [precategory.{v} C] {a b : C} (i : a â‰… b) : b â‰… a :=
  iso.mk i.inv i.hom i.l_inv i.r_inv

/- Calculation rules for isomorphisms. -/
@[hott, hsimp]
def iso_inv_inv {C : Type u} [precategory.{v} C] {a b : C} (i : a â‰… b) :
  (inv_iso i)â»Â¹Ê° = i.hom :=
by hsimp 

@[hott, hsimp]
def iso_rcancel {C : Type u} [precategory.{v} C] {a b c : C} (i : a â‰… b)
  {g h : c âŸ¶ a} : g â‰« i.hom = h â‰« i.hom -> g = h :=
assume pc, 
have pc_inv : (g â‰« i.hom) â‰« i.inv = (h â‰« i.hom) â‰« i.inv, from 
  ap (Î» h : c âŸ¶ b, h â‰« i.inv) pc,
calc   g = g â‰« ğŸ™ a : by hsimp
     ... = g â‰« (i.hom â‰« i.inv) : by rwr <-i.l_inv
     ... = (g â‰« i.hom) â‰« i.inv : by hsimp
     ... = (h â‰« i.hom) â‰« i.inv : by rwr pc_inv
     ... = h â‰« (i.hom â‰« i.inv) : by hsimp
     ... = h â‰« ğŸ™ a : by rwr i.l_inv     
     ... = h : by hsimp 

@[hott, hsimp]
def iso_lcancel {C : Type u} [precategory.{v} C] {a b c : C} (i : a â‰… b)
  {g h : b âŸ¶ c} : i.hom â‰« g = i.hom â‰« h -> g = h :=
assume cp, 
have cp_inv : i.inv â‰« (i.hom â‰« g) = i.inv â‰« (i.hom â‰« h), from 
  ap (Î» h : a âŸ¶ c, i.inv â‰« h) cp,
calc   g = ğŸ™ b â‰« g : by hsimp
     ... = (i.inv â‰« i.hom) â‰« g : by rwr <-i.r_inv
     ... = i.inv â‰« (i.hom â‰« g) : by hsimp
     ... = i.inv â‰« (i.hom â‰« h) : by rwr cp_inv
     ... = (i.inv â‰« i.hom) â‰« h : by hsimp
     ... = ğŸ™ b â‰« h : by rwr i.r_inv     
     ... = h : by hsimp 

@[hott, hsimp]
def iso_move_lr {C : Type u} [precategory.{v} C] {a b c : C} (i : a â‰… b)
  (g : b âŸ¶ c) (h : a âŸ¶ c) : i.hom â‰« g = h -> g = i.inv â‰« h :=
assume pcr,
have i.inv â‰« i.hom â‰« g = i.inv â‰« h, from ap (Î» h : a âŸ¶ c, i.inv â‰« h) pcr,
calc g   = ğŸ™ b â‰« g : by hsimp
     ... = (i.inv â‰« i.hom) â‰« g : by rwr <-i.r_inv
     ... = i.inv â‰« (i.hom â‰« g) : by hsimp
     ... = i.inv â‰« h : by rwr pcr   

@[hott, hsimp]
def iso_move_rl {C : Type u} [precategory.{v} C] {a b c : C} (i : a â‰… b)
  (g : c âŸ¶ a) (h : c âŸ¶ b) : g â‰« i.hom = h -> g = h â‰« i.inv :=
assume pcl,
have (g â‰« i.hom) â‰« i.inv = h â‰« i.inv, from ap (Î» h : c âŸ¶ b, h â‰« i.inv) pcl,
calc g   = g â‰« ğŸ™ a : by hsimp
     ... = g â‰« (i.hom â‰« i.inv) : by rwr <-i.l_inv
     ... = (g â‰« i.hom) â‰« i.inv : by hsimp
     ... = h â‰« i.inv : by rwr pcl     

/- Isomorphisms are uniquely determined by their underlying homomorphism:
   The inverse map by functorial equalities, and the functorial equalities 
   because the types of homomorphisms are sets. -/
@[hott]
def hom_eq_to_iso_eq {C : Type u} [precategory.{v} C] {a b : C} {i j : a â‰… b} :
  i.hom = j.hom -> i = j :=
assume hom_eq, 
have inv_eq : i.inv = j.inv, from 
  calc i.inv = i.inv â‰« ğŸ™ a : by hsimp
       ...   = i.inv â‰« (j.hom â‰« j.inv) : by rwr j.l_invâ»Â¹ 
       ...   = (i.inv â‰« j.hom) â‰« j.inv : by hsimp
       ...   = (i.inv â‰« i.hom) â‰« j.inv : by rwr hom_eqâ»Â¹
       ...   = ğŸ™ b â‰« j.inv : by rwr i.r_inv
       ...   = j.inv : by hsimp,
let R := Î» (f : a âŸ¶ b) (g : b âŸ¶ a), g â‰« f = ğŸ™ b,
    L := Î» (f : a âŸ¶ b) (g : b âŸ¶ a), f â‰« g = ğŸ™ a in
have r_inv_eq : i.r_inv =[ap011 R hom_eq inv_eq; id] j.r_inv, from 
  begin apply pathover_of_tr_eq, apply is_set.elim end,
have l_inv_eq : i.l_inv =[ap011 L hom_eq inv_eq; id] j.l_inv, from 
  begin apply pathover_of_tr_eq, apply is_set.elim end, 
calc   i = iso.mk i.hom i.inv i.r_inv i.l_inv : iso.eta i 
     ... = iso.mk j.hom j.inv j.r_inv j.l_inv : 
                                        ap0111 iso.mk hom_eq inv_eq r_inv_eq l_inv_eq
     ... = j : (iso.eta j)â»Â¹

@[hott, hsimp]
def id_is_iso {C : Type u} [precategory.{v} C] (a : C) : a â‰… a := 
  have inv_eq : ğŸ™ a â‰« ğŸ™ a = ğŸ™ a, from precategory.id_comp (ğŸ™ a),
  iso.mk (ğŸ™ a) (ğŸ™ a) inv_eq inv_eq

@[hott, hsimp]
def idtoiso {C : Type u} [precategory.{v} C] {a b : C} : (a = b) -> (a â‰… b) :=
  begin intro eq, exact eq â–¸[Î» c, a â‰… c] id_is_iso a end

/- `idtoiso` is natural. -/
@[hott, hsimp]
def idtoiso_refl_eq {C : Type u} [precategory.{v} C] (a : C) : idtoiso (refl a) = id_is_iso a :=
  by hsimp

@[hott]
def id_inv_iso_inv {C : Type u} [precategory.{v} C] {câ‚ câ‚‚ : C} (p : câ‚ = câ‚‚) :
  idtoiso pâ»Â¹ = inv_iso (idtoiso p) := 
begin hinduction p, refl end 

/- The next two facts correspond to [HoTT-Book, Lem.9.1.9]. -/
@[hott]
def id_hom_tr_comp {C : Type u} [precategory.{v} C] {câ‚ câ‚‚ d : C} (p : câ‚ = câ‚‚)
  (h : câ‚ âŸ¶ d) : p â–¸ h = (idtoiso p)â»Â¹Ê° â‰« h :=
begin hinduction p, hsimp end   

@[hott]
def id_hom_tr_comp' {C : Type u} [precategory.{v} C] {câ‚ câ‚‚ d : C} (p : câ‚ = câ‚‚)
  (h : d âŸ¶ câ‚) : p â–¸ h = h â‰« (idtoiso p).hom :=
begin hinduction p, hsimp end 

/-- The structure of a category. -/
@[hott]
class category (obj : Type u) extends precategory.{v} obj :=
(ideqviso : âˆ€ a b : obj, is_equiv (@idtoiso _ _ a b)) 

attribute [instance] category.ideqviso

@[hott, hsimp]
def category.isotoid {obj : Type u} [category.{v} obj] : 
  Î  {a b : obj}, a â‰… b -> a = b :=
assume a b iso,  
@is_equiv.inv _ _ _ (category.ideqviso a b) iso  

@[hott, hsimp]
def category.idtoiso_rinv {obj : Type u} [category.{v} obj] {a b : obj} :
  âˆ€ i : a â‰… b, idtoiso (idtoisoâ»Â¹á¶  i) = i :=
is_equiv.right_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_linv {obj : Type u} [category.{v} obj] {a b : obj} :
  âˆ€ p : a = b, idtoisoâ»Â¹á¶  (idtoiso p) = p :=
is_equiv.left_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_rinv' {obj : Type u} [category.{v} obj] {a b : obj} :
  âˆ€ i : a â‰… b, idtoiso (category.isotoid i) = i :=
is_equiv.right_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_linv' {obj : Type u} [category.{v} obj] {a b : obj} :
  âˆ€ p : a = b, category.isotoid (idtoiso p) = p :=
is_equiv.left_inv (@idtoiso _ _ a b) 

@[hott]
def isotoid_id_refl {obj : Type u} [category.{v} obj] :
  Î  (a : obj), category.isotoid (id_is_iso a) = refl a :=
begin intro a, rwr <- idtoiso_refl_eq a, exact category.idtoiso_linv (refl a) end 

@[hott]
def iso_hom_tr_comp {C : Type u} [category.{v} C] {câ‚ câ‚‚ d : C} (i : câ‚ â‰… câ‚‚)
  (h : câ‚ âŸ¶ d) : (idtoisoâ»Â¹á¶  i) â–¸ h = iâ»Â¹Ê° â‰« h :=
begin 
  rwr <-(category.idtoiso_rinv i),  
  rwr category.idtoiso_linv (idtoisoâ»Â¹á¶  i),
  exact id_hom_tr_comp (idtoisoâ»Â¹á¶  i) h
end 

@[hott]
def iso_hom_tr_comp' {C : Type u} [category.{v} C] {câ‚ câ‚‚ d : C} (i : câ‚ â‰… câ‚‚)
  (h : d âŸ¶ câ‚) : (idtoisoâ»Â¹á¶  i) â–¸ h = h â‰« i.hom :=
begin 
  rwr <-(category.idtoiso_rinv i),  
  rwr category.idtoiso_linv (idtoisoâ»Â¹á¶  i),
  exact id_hom_tr_comp' (idtoisoâ»Â¹á¶  i) h
end 


/- In a category `C` we can define a subobject of an object `c` as a monomorphism `a âŸ¶ c`. Two 
   such subobjects are equal if and only if there is an isomorphism between the sources of the 
   monomorphisms factorizing the monomorphisms. Therefore in HoTT categories, it is not necessary 
   to define subobjects as isomorphism classes. -/
@[hott]
def is_mono {C : Type u} [category.{v} C] {câ‚ câ‚‚ : C} (f : câ‚ âŸ¶ câ‚‚) :=
  Î  (d : C) (gâ‚ gâ‚‚ : d âŸ¶ câ‚), gâ‚ â‰« f = gâ‚‚ â‰« f -> gâ‚ = gâ‚‚

@[hott, instance]
def is_mono_is_prop {C : Type u} [category.{v} C] {câ‚ câ‚‚ : C} (f : câ‚ âŸ¶ câ‚‚) : 
  is_prop (is_mono f) :=
begin apply is_prop_dprod, intro d, apply_instance end 

@[hott]
def isos_are_mono {C : Type u} [category.{v} C] {câ‚ câ‚‚ : C} (i : câ‚ â‰… câ‚‚) : is_mono i.hom :=  
  assume d gâ‚ gâ‚‚ eq_comp, 
  calc gâ‚ = gâ‚ â‰« ğŸ™ câ‚ : by rwr precategory.comp_id
       ... = gâ‚ â‰« (i.hom â‰« i.inv) : by rwr iso.l_inv
       ... = (gâ‚ â‰« i.hom) â‰« i.inv : by rwr precategory.assoc
       ... = (gâ‚‚ â‰« i.hom) â‰« i.inv : by rwr eq_comp
       ... = gâ‚‚ : by rwr precategory.assoc; rwr iso.l_inv; rwr precategory.comp_id   

@[hott]
structure hom_of_monos {C : Type u} [category.{v} C] {c dâ‚ dâ‚‚: C} {f : dâ‚ âŸ¶ c} 
  (Hf : is_mono f) {g : dâ‚‚ âŸ¶ c} (Hg : is_mono g) :=
(hom_obj : dâ‚ âŸ¶ dâ‚‚)
(fac : hom_obj â‰« g = f)

@[hott, instance]
def is_prop_hom_of_monos {C : Type u} [category.{v} C] {c dâ‚ dâ‚‚: C} {f : dâ‚ âŸ¶ c} (Hf : is_mono f)
  {g : dâ‚‚ âŸ¶ c} (Hg : is_mono g) : is_prop (hom_of_monos Hf Hg) :=
begin 
  apply is_prop.mk, intros hmâ‚ hmâ‚‚, hinduction hmâ‚ with hâ‚ facâ‚, hinduction hmâ‚‚ with hâ‚‚ facâ‚‚, 
  fapply apd011 (hom_of_monos.mk Hf Hg), 
  { apply Hg, exact facâ‚ â¬ facâ‚‚â»Â¹ },
  { apply pathover_of_tr_eq, exact is_set.elim _ _ } 
end  

@[hott]
structure iso_of_monos {C : Type u} [category.{v} C] {c dâ‚ dâ‚‚: C} {f : dâ‚ âŸ¶ c} (Hf : is_mono f)
  {g : dâ‚‚ âŸ¶ c} (Hg : is_mono g) :=
(iso_obj : dâ‚ â‰… dâ‚‚)
(fac : iso_obj.hom â‰« g = f) 

@[hott]
def iso_of_monos_eq {C : Type u} [category.{v} C] {c dâ‚ dâ‚‚: C} {f : dâ‚ âŸ¶ c} {Hf : is_mono f}
  {g : dâ‚‚ âŸ¶ c} {Hg : is_mono g} (imâ‚ imâ‚‚ : iso_of_monos Hf Hg) : 
  imâ‚.iso_obj = imâ‚‚.iso_obj -> imâ‚ = imâ‚‚ :=
begin 
  hinduction imâ‚ with iso_objâ‚ facâ‚, hinduction imâ‚‚ with iso_objâ‚‚ facâ‚‚, hsimp, 
  intro p, fapply apd011 (iso_of_monos.mk Hf Hg), assumption,
  apply pathover_of_tr_eq, exact is_set.elim _ _ 
end 

@[hott]
def homs_eqv_iso_of_monos {C : Type u} [category.{v} C] {c dâ‚ dâ‚‚: C} {f : dâ‚ âŸ¶ c} (Hf : is_mono f)
  {g : dâ‚‚ âŸ¶ c} (Hg : is_mono g) : 
  (hom_of_monos Hf Hg) Ã— (hom_of_monos Hg Hf) â‰ƒ iso_of_monos Hf Hg :=
begin 
  fapply equiv.mk, 
  { intro homs, let shâ‚ := homs.1, let shâ‚‚ := homs.2, fapply iso_of_monos.mk, 
    { fapply iso.mk, 
      { exact shâ‚.hom_obj },
      { exact shâ‚‚.hom_obj },
      { apply Hg dâ‚‚ (shâ‚‚.hom_obj â‰« shâ‚.hom_obj) (ğŸ™ dâ‚‚), rwr precategory.assoc, 
        rwr shâ‚.fac, rwr shâ‚‚.fac, hsimp },
      { apply Hf dâ‚ (shâ‚.hom_obj â‰« shâ‚‚.hom_obj) (ğŸ™ dâ‚), rwr precategory.assoc, 
        rwr shâ‚‚.fac, rwr shâ‚.fac, hsimp } },
    { hsimp, rwr shâ‚.fac } },
  { fapply adjointify, 
    { intro i, fapply pair, 
      { fapply hom_of_monos.mk, exact i.iso_obj.hom, exact i.fac },
      { fapply hom_of_monos.mk, exact i.iso_obj.inv, rwr iso_move_lr _ _ _ i.fac } },
    { intro im, hinduction im with i fac, apply iso_of_monos_eq _ _, 
      { apply hom_eq_to_iso_eq, hsimp } },
    { intro hm, hinduction hm with hmâ‚ hmâ‚‚, 
      hinduction hmâ‚ with hom_objâ‚ facâ‚, hinduction hmâ‚‚ with hom_objâ‚‚ facâ‚‚, fapply prod.prod_eq,
      { fapply apd011 (hom_of_monos.mk Hf Hg), hsimp, 
        apply pathover_of_tr_eq, exact is_set.elim _ _ },
      { fapply apd011 (hom_of_monos.mk Hg Hf), hsimp, 
        apply pathover_of_tr_eq, exact is_set.elim _ _ } } }
end  

@[hott, instance]
def is_prop_iso_of_monos {C : Type u} [category.{v} C] {c dâ‚ dâ‚‚: C} {f : dâ‚ âŸ¶ c} (Hf : is_mono f)
  {g : dâ‚‚ âŸ¶ c} (Hg : is_mono g) : is_prop (iso_of_monos Hf Hg) :=
begin apply is_trunc_equiv_closed -1 (homs_eqv_iso_of_monos Hf Hg), apply_instance end

@[hott]
structure subobject {C : Type u} [category.{v} C] (c : C) :=
  (obj : C)
  (hom : obj âŸ¶ c)
  (is_mono : is_mono hom)  

@[hott]
def subobject_eta {C : Type u} [category.{v} C] {c : C} (so : subobject c) :
  so = subobject.mk so.obj so.hom so.is_mono :=
begin hinduction so, refl end   

@[hott]
def subobject_eta_eq {C : Type u} [category.{v} C] {c : C} (obj : C) (hom : obj âŸ¶ c) 
  (is_mono : is_mono hom) : subobject_eta (subobject.mk obj hom is_mono) = idp :=
rfl  

@[hott] 
def subobject_eq_idp {C : Type u} [category.{v} C] {c : C} {s : subobject c} 
  (p : s.obj = s.obj) (q : s.hom =[p; Î» d, d âŸ¶ c] s.hom) 
  (r : s.is_mono =[apd011 (Î» (a : C) (b : â†¥(a âŸ¶ c)), is_mono b) p q; id] s.is_mono) :
  p = idp -> apd0111 subobject.mk p q r = idp :=
begin 
  intro Hp, 
  have Hq : q =[Hp; Î» p' : s.obj = s.obj, s.hom =[p'; Î» d, d âŸ¶ c] s.hom] idpatho s.hom, from 
    begin apply pathover_of_tr_eq, exact set_po_eq _ _ end,
  have H : is_prop (s.is_mono =[idp; id] s.is_mono), from 
    begin 
      apply is_trunc_equiv_closed_rev -1 (pathover_equiv_tr_eq _ _ _), exact is_trunc_eq -1 _ _
     end,  
  have Hr : r =[apd011 (Î» (x : s.obj = s.obj) (y : s.hom =[x; Î» d, d âŸ¶ c] s.hom), 
                          apd011 (Î» (obj : C) (hom : obj âŸ¶ c), is_mono hom) x y) Hp Hq;
                Î» Hf : is_mono s.hom = is_mono s.hom, s.is_mono =[Hf; id] s.is_mono] 
                                                                      idpatho s.is_mono, from 
    begin apply pathover_of_tr_eq, exact @is_prop.elim _ H _ _ end, 
  rwr @apd0111_eq _ _ _ (Î» (obj : C) (hom : obj âŸ¶ c), is_mono hom) _ _ _ _ _ _ _ _ _ _ _ _ _ 
                                                                                       Hp Hq Hr 
end   

/- A homomorphism between subobjects compatible with the injections is itself injection. Hence,
   homomorphisms between subobjects in both ways imply an isomorphism of subobjects and therefore
   equality. -/
@[hott]
def subobject_hom {C : Type u} [category.{v} C] {c : C} (sâ‚ sâ‚‚ : subobject c) :=
  hom_of_monos sâ‚.is_mono sâ‚‚.is_mono

@[hott, instance]
def subobject_hom_is_prop {C : Type u} [category.{v} C] {c : C} (sâ‚ sâ‚‚ : subobject c) :
  is_prop (subobject_hom sâ‚ sâ‚‚) :=
begin change is_prop (hom_of_monos sâ‚.is_mono sâ‚‚.is_mono), apply_instance end    

@[hott]
def equal_subobj_to_iso_mono {C : Type u} [category.{v} C] {c : C} (sâ‚ sâ‚‚ : subobject c) :
  sâ‚ = sâ‚‚ -> iso_of_monos sâ‚.is_mono sâ‚‚.is_mono :=
begin 
  intro p, fapply iso_of_monos.mk, 
  exact (idtoiso (ap subobject.obj p)), 
  hinduction p, hsimp 
end  

@[hott] 
def idp_subobj_to_iso_mono {C : Type u} [category.{v} C] {c : C} (s : subobject c) :
  equal_subobj_to_iso_mono s s idp = iso_of_monos.mk s.is_mono s.is_mono (id_is_iso s.obj) 
                                                     (precategory.id_comp s.hom) :=
begin apply iso_of_monos_eq, change idtoiso idp = id_is_iso s.obj, rwr idtoiso_refl_eq end                                                     

@[hott]
def iso_mono_to_equal_subobj {C : Type u} [category.{v} C] {c : C} (sâ‚ sâ‚‚ : subobject c) :
  iso_of_monos sâ‚.is_mono sâ‚‚.is_mono -> sâ‚ = sâ‚‚ :=
assume im, (subobject_eta sâ‚) â¬  
begin 
  fapply apd0111 subobject.mk, 
  { exact category.isotoid im.iso_obj },
  { apply pathover_of_tr_eq, 
    change idtoisoâ»Â¹á¶  im.iso_obj â–¸[Î» (d : C), â†¥(d âŸ¶ c)] sâ‚.hom = sâ‚‚.hom, 
    rwr iso_hom_tr_comp, 
    calc (im.iso_obj)â»Â¹Ê° â‰« sâ‚.hom = (im.iso_obj)â»Â¹Ê° â‰« im.iso_obj.hom â‰« sâ‚‚.hom : 
                                                                           by rwr im.fac
         ... = ((im.iso_obj)â»Â¹Ê° â‰« im.iso_obj.hom) â‰« sâ‚‚.hom : by rwr precategory.assoc
         ... = ğŸ™ sâ‚‚.obj â‰« sâ‚‚.hom : by rwr iso.r_inv 
         ... = sâ‚‚.hom : by rwr precategory.id_comp },
  { apply pathover_of_tr_eq, apply eq_of_homotopy3, intros d gâ‚ gâ‚‚, 
    apply eq_of_homotopy, intro comp_eq, exact is_prop.elim _ _ } 
end 
â¬ (subobject_eta sâ‚‚)â»Â¹  

@[hott]
def iso_mono_to_equal_subobj_iso {C : Type u} [category.{v} C] {c : C} {sâ‚ sâ‚‚ : subobject c} 
  (im : iso_of_monos sâ‚.is_mono sâ‚‚.is_mono) : 
  ap subobject.obj (iso_mono_to_equal_subobj sâ‚ sâ‚‚ im) = category.isotoid im.iso_obj :=
begin
  hinduction sâ‚ with objâ‚ homâ‚ is_monoâ‚, hinduction sâ‚‚ with objâ‚‚ homâ‚‚ is_monoâ‚‚,
  change ap subobject.obj ((subobject_eta _) â¬ (apd0111 subobject.mk _ _ _) â¬ _) = _, 
  rwr subobject_eta_eq, rwr subobject_eta_eq, rwr idp_con, rwr idp_inv, rwr con_idp,
  let HP : Î  (obj : C) (hom : obj âŸ¶ c) (is_mono : is_mono hom), 
                                     subobject.obj (subobject.mk obj hom is_mono) = obj := 
      begin intros obj hom is_mono, exact idp end, 
  rwr ap_apd0111 subobject.mk _ _ _ subobject.obj HP, 
  change idp â¬ category.isotoid im.iso_obj â¬ idpâ»Â¹ = _, rwr idp_con   
end    

@[hott]
def equal_subobj_eqv_iso_mono {C : Type u} [category.{v} C] {c : C} (sâ‚ sâ‚‚ : subobject c) :
  sâ‚ = sâ‚‚ â‰ƒ iso_of_monos sâ‚.is_mono sâ‚‚.is_mono :=
begin
  fapply equiv.mk,
  { exact equal_subobj_to_iso_mono sâ‚ sâ‚‚ },
  { fapply adjointify,
    { exact iso_mono_to_equal_subobj sâ‚ sâ‚‚ },
    { hinduction sâ‚ with objâ‚ homâ‚ is_monoâ‚, hinduction sâ‚‚ with objâ‚‚ homâ‚‚ is_monoâ‚‚,
      intro im, hinduction im with iso_obj fac, apply iso_of_monos_eq _ _, hsimp,
      change idtoiso (ap subobject.obj _) = _, rwr iso_mono_to_equal_subobj_iso,
      change idtoiso (idtoisoâ»Â¹á¶  _) = _, rwr category.idtoiso_rinv },
    { intro p, hinduction p, --hinduction sâ‚ with objâ‚ homâ‚ is_monoâ‚, 
      rwr idp_subobj_to_iso_mono, 
      change (subobject_eta _) â¬ (apd0111 subobject.mk (category.isotoid (id_is_iso sâ‚.obj)) _ _) â¬ 
                                                                                              _ = _, 
      apply con_eq_of_eq_con_inv, apply con_eq_of_eq_inv_con, rwr idp_con, 
      rwr con.right_inv, apply subobject_eq_idp, rwr isotoid_id_refl } }
end    

/- The subobjects of an object in a HoTT-category form a set, so a HoTT-category is well-powered. -/
@[hott, instance]
def subobject_is_set {C : Type u} [category.{v} C] {c : C} : is_set (subobject c) :=
begin 
  apply is_trunc_succ_intro, intros sâ‚ sâ‚‚, 
  apply is_trunc_equiv_closed_rev -1 (equal_subobj_eqv_iso_mono sâ‚ sâ‚‚), 
  apply is_trunc_equiv_closed -1 (homs_eqv_iso_of_monos sâ‚.is_mono sâ‚‚.is_mono), 
  apply_instance 
end

section
variables (C : Type u) (D : Type u') (E : Type u'')

/- Functors are defined between precategories. -/
@[hott]
structure functor [precategory.{v} C] [precategory.{v'} D] :
  Type (max v v' u u') :=
(obj      : C â†’ D)
(map      : Î  {x y : C}, (x âŸ¶ y) â†’ ((obj x) âŸ¶ (obj y)))
(map_id   : âˆ€ (x : C), map (ğŸ™ x) = ğŸ™ (obj x))
(map_comp : âˆ€ {x y z : C} (f : x âŸ¶ y) (g : y âŸ¶ z), map (f â‰« g) = (map f) â‰« (map g))

infixr ` â¥¤ ` :26 := functor       

attribute [hsimp] functor.map_id
attribute [hsimp] functor.map_comp

@[hott]
def functor_eta [precategory.{v} C] [precategory.{v'} D] (F : C â¥¤ D) : 
  F = functor.mk F.obj F.map F.map_id F.map_comp :=
begin hinduction F, refl end 

@[hott]
def functor_eta_mk [precategory.{v} C] [precategory.{v'} D] :
  Î  obj map map_id map_comp, functor_eta C D (functor.mk obj map map_id map_comp) = idp :=
assume obj map map_id map_comp, rfl  

@[hott]
def functor_mk_obj [precategory.{v} C] [precategory.{v'} D] :
  Î  obj map map_id map_comp, @functor.obj C D _ _ (functor.mk obj map map_id map_comp) = obj :=
assume obj map map_id map_comp, rfl   

/- Functors are equal if their maps of objects and arrows are equal. -/
@[hott]
def functor_eq [precategory.{v} C] [precategory.{v'} D] {F G : C â¥¤ D} :
  Î  (p : F.obj = G.obj), 
    (F.map =[p; Î» f : C -> D, Î  (x y : C), (x âŸ¶ y) -> (f x âŸ¶ f y)] G.map) -> F = G :=
begin 
  intros p q, 
  exact (functor_eta C D F) â¬ (apd01111_v2 functor.mk p q 
          (pathover_of_tr_eq (is_prop.elim _ _))  (pathover_of_tr_eq (is_prop.elim _ _)))
        â¬ (functor_eta C D G)â»Â¹  
end  

@[hott]
def functor_eq_idp' [precategory.{v} C] [precategory.{v'} D] {obj : C -> D} 
  (map : Î  (câ‚ câ‚‚ : C), (câ‚ âŸ¶ câ‚‚) -> (obj câ‚ âŸ¶ obj câ‚‚)) :
  Î  mi mc, functor_eq C D (@idp _ (functor.mk obj map mi mc).obj) idpo = idp :=
begin 
  intros mi mc,                                          
  change idp â¬ (apd01111_v2 functor.mk idp idpo 
           (pathover_of_tr_eq (is_prop.elim _ _)) (pathover_of_tr_eq (is_prop.elim _ _))) 
         â¬ inverse idp = _, 
  rwr idp_con, rwr idp_inv, rwr con_idp,             
  have H1 : pathover_of_tr_eq (is_prop.elim (apd011 (Î» (a : C â†’ D) 
              (b : Î  {x y : C}, (x âŸ¶ y) â†’ (a x âŸ¶ a y)), Î  (x : C), b (ğŸ™ x) = ğŸ™ (a x))
              idp idpo â–¸[id] mi) mi) = idpo, by apply dep_set_eq_eq,
  have H2 : pathover_of_tr_eq (is_prop.elim (apd011 (Î» (a : C â†’ D) (b : Î  {x y : C}, 
              (x âŸ¶ y) â†’ (a x âŸ¶ a y)), Î  (x y z : C) (f : x âŸ¶ y) (g : y âŸ¶ z), 
              b (f â‰« g) = b f â‰« b g) idp idpo â–¸[id] @mc) @mc) = idpo,
    by apply dep_set_eq_eq,        
  rwr H1, rwr H2
end

@[hott]
def functor_eq_idp [precategory.{v} C] [precategory.{v'} D] {F : C â¥¤ D} :
  functor_eq C D (@idp _ F.obj) idpo = idp :=
begin hinduction F, rwr functor_eq_idp' end

@[hott]
def functor_eq_obj [precategory.{v} C] [precategory.{v'} D] {F G : C â¥¤ D} :
  Î  (p : F.obj = G.obj) q, (ap functor.obj (functor_eq C D p q)) = p :=
begin 
  intros p q, 
  change (ap _ ((functor_eta C D F) â¬ (apd01111_v2 functor.mk p q 
          (pathover_of_tr_eq (is_prop.elim _ _))  (pathover_of_tr_eq (is_prop.elim _ _)))
        â¬ (functor_eta C D G)â»Â¹)) = p, 
  rwr ap_con, rwr ap_con, hinduction F, hinduction G, 
  rwr functor_eta_mk, rwr functor_eta_mk, rwr idp_inv, rwr ap_idp, rwr ap_idp, rwr con_idp,
  rwr idp_con, rwr ap_apd01111_v2 _ _ _ _ _ _ (functor_mk_obj C D),  
  change idp â¬ p â¬ idpâ»Â¹ = p, rwr idp_inv, rwr con_idp, rwr idp_con  
end    

@[hott]
def is_faithful_functor [precategory.{v} C] [precategory.{v'} D] (F : C â¥¤ D) := 
  Î  {x y : C}, is_set_injective (@functor.map C D _ _ F x y) 


@[hott, reducible]
def constant_functor [precategory.{v} C] [precategory.{v'} D] (d : D) : 
  C â¥¤ D := 
have id_hom_eq : âˆ€ d : D, ğŸ™ d = ğŸ™ d â‰« ğŸ™ d, by intro d; hsimp,  
functor.mk (Î» c : C, d) (Î» câ‚ câ‚‚ f, ğŸ™ d) (Î» c, rfl) 
  (Î» câ‚ câ‚‚ câ‚ƒ f g, (id_hom_eq d))

@[hott]
def constant_functor_map [precategory.{v} C] [precategory.{v'} D] (d : D) :
  âˆ€ {câ‚ câ‚‚ : C} (h : câ‚ âŸ¶ câ‚‚), (constant_functor C D d).map h = ğŸ™ d :=
assume câ‚ câ‚‚ h, rfl  

@[hott, reducible]
def id_functor [precategory.{v} C] : C â¥¤ C :=
  functor.mk (Î» c : C, c) (Î» câ‚ câ‚‚ f, f) (Î» c, idp) (Î» câ‚ câ‚‚ câ‚ƒ f g, idp)  


@[hott]
structure nat_trans [precategory.{v} C] [precategory.{v'} D] (F G : C â¥¤ D) :=
(app : Î  c : C, (F.obj c) âŸ¶ (G.obj c))
(naturality : âˆ€ {c c' : C} (f : c âŸ¶ c'), 
                                 (F.map f) â‰« (app c') = (app c) â‰« (G.map f))  

infixr ` âŸ¹ `:10 := nat_trans _ _

end

section
variables {C : Type u} {D : Type u'} {E : Type u''}

/- The composition of functors -/
@[hott, reducible]
def functor_comp [precategory.{v} C] [precategory.{v'} D] [precategory.{v''} E]
  (F : C â¥¤ D) (G : D â¥¤ E) : C â¥¤ E := 
begin
  fapply functor.mk,  
  { exact Î» c : C, G.obj (F.obj c) }, -- map of objects
  { intros c c' f, exact G.map (F.map f) },  -- map of morphisms
  { intro x, hsimp }, -- identity morphisms are preserved
  { intros x y x f g, hsimp } --composition of morphisms is preserved
end  

infixr ` â‹™ `:25 := functor_comp 

end 

/- The fully embedded category of a type injectively mapped to a category. 
   We start with a synonym for an (embedded) type `D`, on which the category structure
   will be defined, as in [category_theory.full_subcategory] of the mathlib. -/
@[hott]
def ind_cat_type {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C) := D

@[hott, instance]
def mapped_type_has_hom {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C) : 
  has_hom (ind_cat_type f) :=
begin fapply has_hom.mk, intros dâ‚ dâ‚‚, exact f dâ‚ âŸ¶ f dâ‚‚ end  

@[hott]
def ind_type_hom_hom {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C)
  {dâ‚ dâ‚‚ : ind_cat_type f} : (dâ‚ âŸ¶ dâ‚‚) -> (f dâ‚ âŸ¶ f dâ‚‚) := 
assume h, h  

@[hott, instance]
def ind_type_cat_struct {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C) : 
  category_struct (ind_cat_type f) :=
begin
  fapply category_struct.mk,
  { intro a, exact ğŸ™ (f a) },
  { intros a b c f g, exact f â‰« g }
end  

@[hott, instance]
def fully_ind_precategory {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C) : 
  precategory (ind_cat_type f) :=
begin
  fapply precategory.mk,
  { intros dâ‚ dâ‚‚ f, hsimp },
  { intros dâ‚ dâ‚‚ f, hsimp },
  { intros dâ‚ dâ‚‚ dâ‚ƒ dâ‚„ f g h, hsimp, refl }
end  

@[hott]
def ind_type_iso_iso {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C)
  {dâ‚ dâ‚‚ : ind_cat_type f} : (dâ‚ â‰… dâ‚‚) -> (f dâ‚ â‰… f dâ‚‚) := 
begin
  intro i, fapply iso.mk,  
  { exact i.hom },
  { exact i.inv },
  { exact i.r_inv },
  { exact i.l_inv }
end  

@[hott]
def ind_idtoiso_hom {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C)
  (inj : is_injective (Î» d : ind_cat_type f, f d)) {dâ‚ dâ‚‚ : ind_cat_type f} : 
  Î  p : f dâ‚ = f dâ‚‚, (idtoiso (inj_imp inj dâ‚ dâ‚‚ p)).hom = (idtoiso p).hom :=
begin 
  fapply equiv_arg_exchange,
  { exact dâ‚ = dâ‚‚ },
  { intro p, exact ap f p },
  { exact inj dâ‚ dâ‚‚ },
  { intro q, fapply @eq.rec _ dâ‚ (Î» dâ‚‚, Î» q : dâ‚ = dâ‚‚, 
               (idtoiso (inj_imp inj dâ‚ dâ‚‚ (ap f q))).hom = (idtoiso (ap f q)).hom), 
    change (idtoiso (inj_imp inj dâ‚ dâ‚ (ap f (refl dâ‚)))).hom = ğŸ™ dâ‚, 
    have H : inj_imp inj dâ‚ dâ‚ (ap f (refl dâ‚)) = refl dâ‚, from
      @is_equiv.left_inv _ _ _ (inj dâ‚ dâ‚) (refl dâ‚), 
    rwr H }
end

@[hott, instance]
def fully_embedded_category {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C)
  [inj : is_injective f] : category (ind_cat_type f) :=
begin
  fapply category.mk,
  intros dâ‚ dâ‚‚, fapply adjointify, 
  { intro i, exact inj_imp inj dâ‚ dâ‚‚ (category.isotoid (ind_type_iso_iso f i)) },
  { intro i, apply hom_eq_to_iso_eq, 
    rwr ind_idtoiso_hom f inj (category.isotoid (ind_type_iso_iso f i)),
    change (idtoiso (idtoisoâ»Â¹á¶  (ind_type_iso_iso f i))).hom = i.hom,
    rwr category.idtoiso_rinv (ind_type_iso_iso f i) },
  { intro p, hinduction p, rwr idtoiso_refl_eq dâ‚, 
    have H : ind_type_iso_iso f (id_is_iso dâ‚) = id_is_iso (f dâ‚), from 
      begin apply hom_eq_to_iso_eq, refl end,
    rwr H, rwr isotoid_id_refl, exact inj_idp dâ‚ }
end    

@[hott]
def emb_functor {C : Type u} [category.{v} C] {D : Type u'} (f : D -> C) : 
  (ind_cat_type f) â¥¤ C :=
begin
  fapply functor.mk,
  { exact f },
  { intros x y h, exact h },
  { intro x, refl },
  { intros x y z g h, refl }
end  

/- The full subcategory on a subtype of the type of a category can be defined using
   the injctive embedding of the subtype into the type. -/
@[hott]
def subtype_emb {C : Type u} [category.{v} C] (P : C -> trunctype.{0} -1) :
  subtype (Î» c : C, â†¥(P c)) -> C := assume sc, sc.1

@[hott, instance]
def subtype_emb_is_inj {C : Type u} [category.{v} C] (P : C -> trunctype.{0} -1) :
  is_injective (subtype_emb P) :=
begin intros scâ‚ scâ‚‚, exact (subtype_eq_equiv scâ‚ scâ‚‚).to_is_equiv end    

@[hott, instance]
def full_subcat_on_subtype {C : Type u} [category.{v} C] (P : C -> trunctype.{0} -1) :
  category (subtype (Î» c : C, â†¥(P c))) :=
@fully_embedded_category _ _ _ (subtype_emb P) (subtype_emb_is_inj P)  

@[hott]
def embed {C : Type u} [category.{v} C] {P : C -> trunctype.{0} -1} 
  {D : Type u'} [precategory.{v'} D] (F : D â¥¤ subtype (Î» c : C, â†¥(P c))) : D â¥¤ C :=
F â‹™ (emb_functor (subtype_emb P))   

end categories

end hott