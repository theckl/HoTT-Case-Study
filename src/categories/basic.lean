import sets.algebra init2 types2 sets.axioms categories.precat

universes v v' v'' v''' u u' u'' u''' w 
hott_theory

namespace hott
open hott.eq hott.sigma hott.set hott.subset hott.is_trunc 
     hott.is_equiv hott.precategories

/-
We introduce precategories and categories following the HoTT book, 
Sec. 9.1. HoTT precategories have sets of homomorphisms, and HoTT categories 
prescribe univalence : Isomorphisms are equivalent to identities of objects.

As far as possible we copy the mathlib-code in [category_theory.category.default]. In particular,
we keep the distinction of universe levels for objects and morphisms of a category.
-/

namespace categories

/- Definition of categorical isomorphisms. -/
@[hott]
structure is_iso {C : Type u} [is_precat.{v} C] {a b : C} (f : a âŸ¶ b) :=
  (inv : b âŸ¶ a)
  (r_inv : inv â‰« f = ğŸ™ b)
  (l_inv : f â‰« inv = ğŸ™ a)

@[hott, instance]
def is_iso_is_prop {C : Type u} [HP :is_precat.{v} C] {a b : C} 
  (f : a âŸ¶ b) : is_prop (is_iso f) :=
begin
  apply is_prop.mk, intros is_isoâ‚ is_isoâ‚‚,
  hinduction is_isoâ‚ with invâ‚ r_invâ‚ l_invâ‚,
  hinduction is_isoâ‚‚ with invâ‚‚ r_invâ‚‚ l_invâ‚‚,
  fapply apd0111' is_iso.mk, 
  { rwr <- @is_precat.comp_id _ HP _ _ invâ‚, rwr <- l_invâ‚‚,
    rwr <- @is_precat.assoc _ HP, rwr r_invâ‚, 
    rwr @is_precat.id_comp _ HP _ _ invâ‚‚ },
  all_goals { apply pathover_of_tr_eq, exact is_prop.elim _ _ }
end

@[hott]
structure iso {C : Type u} [is_precat.{v} C] (a b : C) :=
  (hom : a âŸ¶ b)
  (ih : is_iso hom)

infix ` â‰… `:25 := iso

@[hott]
instance iso_to_hom {C : Type u} [is_precat.{v} C] (a b : C) : 
  has_coe_to_fun (a â‰… b) :=
has_coe_to_fun.mk (Î» _, a âŸ¶ b) (Î» i, i.hom)

@[hott, hsimp]
def inv_iso {C : Type u} [is_precat.{v} C] {a b : C} (i : a â‰… b) : b â‰… a :=
  iso.mk i.ih.inv 
         (is_iso.mk i.hom i.ih.l_inv i.ih.r_inv)

postfix `â»Â¹Ê°`:std.prec.max_plus := inv_iso 

/- Calculation rules for isomorphisms. -/
@[hott, hsimp]
def iso_inv_inv {C : Type u} [is_precat.{v} C] {a b : C} (i : a â‰… b) :
  (inv_iso i)â»Â¹Ê° = i :=
by hinduction i with hom iso_hom; hinduction iso_hom; hsimp 

@[hott, hsimp]
def iso_rcancel {C : Type u} [is_precat.{v} C] {a b c : C} (i : a â‰… b)
  {g h : c âŸ¶ a} : g â‰« i.hom = h â‰« i.hom -> g = h :=
assume pc, 
have pc_inv : (g â‰« i.hom) â‰« i.ih.inv = (h â‰« i.hom) â‰« i.ih.inv, from 
  ap (Î» h : c âŸ¶ b, h â‰« i.ih.inv) pc,
calc   g = g â‰« ğŸ™ a : by hsimp
     ... = g â‰« (i.hom â‰« i.ih.inv) : by rwr <- i.ih.l_inv
     ... = (g â‰« i.hom) â‰« i.ih.inv : by hsimp
     ... = (h â‰« i.hom) â‰« i.ih.inv : by rwr pc_inv
     ... = h â‰« (i.hom â‰« i.ih.inv) : by hsimp
     ... = h â‰« ğŸ™ a : by rwr i.ih.l_inv     
     ... = h : by hsimp 

@[hott, hsimp]
def iso_lcancel {C : Type u} [is_precat.{v} C] {a b c : C} (i : a â‰… b)
  {g h : b âŸ¶ c} : i.hom â‰« g = i.hom â‰« h -> g = h :=
assume cp, 
have cp_inv : i.ih.inv â‰« (i.hom â‰« g) = i.ih.inv â‰« (i.hom â‰« h), from 
  ap (Î» h : a âŸ¶ c, i.ih.inv â‰« h) cp,
calc   g = ğŸ™ b â‰« g : by hsimp
     ... = (i.ih.inv â‰« i.hom) â‰« g : by rwr <-i.ih.r_inv
     ... = i.ih.inv â‰« (i.hom â‰« g) : by hsimp
     ... = i.ih.inv â‰« (i.hom â‰« h) : by rwr cp_inv
     ... = (i.ih.inv â‰« i.hom) â‰« h : by hsimp
     ... = ğŸ™ b â‰« h : by rwr i.ih.r_inv     
     ... = h : by hsimp 

@[hott, hsimp]
def iso_move_lr {C : Type u} [is_precat.{v} C] {a b c : C} (i : a â‰… b)
  (g : b âŸ¶ c) (h : a âŸ¶ c) : i.hom â‰« g = h -> g = i.ih.inv â‰« h :=
assume pcr,
have i.ih.inv â‰« i.hom â‰« g = i.ih.inv â‰« h, from 
  ap (Î» h : a âŸ¶ c, i.ih.inv â‰« h) pcr,
calc g   = ğŸ™ b â‰« g : by hsimp
     ... = (i.ih.inv â‰« i.hom) â‰« g : by rwr <- i.ih.r_inv
     ... = i.ih.inv â‰« (i.hom â‰« g) : by hsimp
     ... = i.ih.inv â‰« h : by rwr pcr   

@[hott, hsimp]
def iso_move_rl {C : Type u} [is_precat.{v} C] {a b c : C} (i : a â‰… b)
  (g : c âŸ¶ a) (h : c âŸ¶ b) : g â‰« i.hom = h -> g = h â‰« i.ih.inv :=
assume pcl,
have (g â‰« i.hom) â‰« i.ih.inv = h â‰« i.ih.inv, from 
  ap (Î» h : c âŸ¶ b, h â‰« i.ih.inv) pcl,
calc g   = g â‰« ğŸ™ a : by hsimp
     ... = g â‰« (i.hom â‰« i.ih.inv) : by rwr <-i.ih.l_inv
     ... = (g â‰« i.hom) â‰« i.ih.inv : by hsimp
     ... = h â‰« i.ih.inv : by rwr pcl     

/- Isomorphisms are uniquely determined by their underlying homomorphism:
   The inverse map by functorial equalities, and the functorial equalities 
   because the types of homomorphisms are sets. -/
@[hott]
def hom_eq_to_iso_eq {C : Type u} [is_precat.{v} C] {a b : C} {i j : a â‰… b} :
  i.hom = j.hom -> i = j :=
begin
  hinduction i, hinduction j,
  intro hom_eq, fapply apd011 iso.mk, 
  exact hom_eq, apply pathover_of_tr_eq, exact is_prop.elim _ _
end

@[hott, hsimp]
def id_iso {C : Type u} [is_precat.{v} C] (a : C) : a â‰… a := 
  have inv_eq : ğŸ™ a â‰« ğŸ™ a = ğŸ™ a, from is_precat.id_comp (ğŸ™ a),
  iso.mk (ğŸ™ a) (is_iso.mk (ğŸ™ a) inv_eq inv_eq)

@[hott, hsimp]
def idtoiso {C : Type u} [is_precat.{v} C] {a b : C} : (a = b) -> (a â‰… b) :=
  begin intro eq, exact eq â–¸[Î» c, a â‰… c] id_iso a end

/- `idtoiso` is natural. -/
@[hott, hsimp]
def idtoiso_refl_eq {C : Type u} [is_precat.{v} C] (a : C) : idtoiso (refl a) = id_iso a :=
  by hsimp

@[hott]
def id_inv_iso_inv {C : Type u} [is_precat.{v} C] {câ‚ câ‚‚ : C} (p : câ‚ = câ‚‚) :
  idtoiso pâ»Â¹ = inv_iso (idtoiso p) := 
begin hinduction p, refl end 

@[hott]
def idtoiso_comp_eq {C : Type u} [is_precat.{v} C] {câ‚ câ‚‚ câ‚ƒ : C} 
  (p : câ‚ = câ‚‚) (q : câ‚‚ = câ‚ƒ) : 
  (idtoiso p).hom â‰« (idtoiso q).hom = (idtoiso (p â¬ q)).hom :=
begin hinduction p, hinduction q, hsimp end   

/- `idtoiso` commutes with functors -/
@[hott]
def funct_idtoiso {C : Type _} [is_precat C] {câ‚ câ‚‚ : C} 
  {D : Type _} [is_precat D] (F : C â¥¤ D) (p : câ‚ = câ‚‚) :
  F.map (idtoiso p).hom = (idtoiso (ap F.obj p)).hom :=
begin 
  hinduction p, rwr idtoiso_refl_eq, rwr ap_idp, 
  rwr idtoiso_refl_eq, change F.map (ğŸ™ câ‚) = _, rwr F.map_id 
end  

/- The next two facts correspond to [HoTT-Book, Lem.9.1.9]. -/
@[hott]
def id_hom_tr_comp {C : Type u} [is_precat.{v} C] {câ‚ câ‚‚ d : C} 
  (p : câ‚ = câ‚‚) (h : câ‚ âŸ¶ d) : p â–¸ h = (idtoiso p)â»Â¹Ê°.hom â‰« h :=
begin hinduction p, hsimp end   

@[hott]
def id_hom_tr_comp' {C : Type u} [is_precat.{v} C] {câ‚ câ‚‚ d : C}
  (p : câ‚ = câ‚‚) (h : d âŸ¶ câ‚) : p â–¸ h = h â‰« (idtoiso p).hom :=
begin hinduction p, hsimp end 

/- We can use `idtoiso` to describe equality of the maps on 
   homorphisms induced by an equality of functors. -/
@[hott]
def functor_map_eq {C : Type _} [is_precat C] {D : Type _} 
  [is_precat D] {F G : C â¥¤ D} (p : F = G) {x y : C} : Î  (f : x âŸ¶ y), 
  G.map f = (idtoiso (ap10 (ap functor.obj p) x)â»Â¹).hom â‰«
            F.map f â‰« (idtoiso (ap10 (ap functor.obj p) y)).hom :=   
begin intro f, hinduction p, hsimp end

/-- The structure of a category and the bundled category. -/
@[hott]
class is_cat (obj : Type u) extends is_precat.{v} obj :=
(ideqviso : âˆ€ a b : obj, is_equiv (@idtoiso _ _ a b)) 

attribute [instance] is_cat.ideqviso

@[hott]
structure Category :=
  (obj : Type u)
  (struct : is_cat obj)

@[hott] instance : has_coe_to_sort Category := 
  has_coe_to_sort.mk Type.{u} Category.obj

@[hott] 
def Cat.to_Precat : Category -> Precategory :=
  Î» C, Precategory.mk C.obj C.struct.to_is_precat

attribute [instance] Category.struct

@[hott, hsimp]
def category.isotoid {C : Category} : 
  Î  {a b : C}, a â‰… b -> a = b :=
assume a b iso,  
@is_equiv.inv _ _ _ (is_cat.ideqviso a b) iso  

@[hott, hsimp]
def category.idtoiso_rinv {C : Category} {a b : C} :
  âˆ€ i : a â‰… b, idtoiso (idtoisoâ»Â¹á¶  i) = i :=
is_equiv.right_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_linv {C : Category} {a b : C} :
  âˆ€ p : a = b, idtoisoâ»Â¹á¶  (idtoiso p) = p :=
is_equiv.left_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_rinv' {C : Category} {a b : C} :
  âˆ€ i : a â‰… b, idtoiso (category.isotoid i) = i :=
is_equiv.right_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_linv' {C : Category} {a b : C} :
  âˆ€ p : a = b, category.isotoid (idtoiso p) = p :=
is_equiv.left_inv (@idtoiso _ _ a b) 

@[hott]
def isotoid_id_refl {C : Category} :
  Î  (a : C), category.isotoid (id_iso a) = refl a :=
begin intro a, rwr <- idtoiso_refl_eq a, exact category.idtoiso_linv (refl a) end 

@[hott]
def iso_hom_tr_comp {C : Category} {câ‚ câ‚‚ d : C} (i : câ‚ â‰… câ‚‚)
  (h : câ‚ âŸ¶ d) : (idtoisoâ»Â¹á¶  i) â–¸ h = iâ»Â¹Ê°.hom â‰« h :=
begin 
  rwr <-(category.idtoiso_rinv i),  
  rwr category.idtoiso_linv (idtoisoâ»Â¹á¶  i),
  exact id_hom_tr_comp (idtoisoâ»Â¹á¶  i) h
end 

@[hott]
def iso_hom_tr_comp' {C : Category} {câ‚ câ‚‚ d : C} (i : câ‚ â‰… câ‚‚)
  (h : d âŸ¶ câ‚) : (idtoisoâ»Â¹á¶  i) â–¸ h = h â‰« i.hom :=
begin 
  rwr <-(category.idtoiso_rinv i),  
  rwr category.idtoiso_linv (idtoisoâ»Â¹á¶  i),
  exact id_hom_tr_comp' (idtoisoâ»Â¹á¶  i) h
end 

/- A modified criterion for equality of functors -/
@[hott]
def functor_eq' {A : Type _} [is_precat A] {B : Type _} [is_precat B] 
  {F G : A â¥¤ B} : Î  (p : Î  (x : A), F.obj x = G.obj x), 
    (Î  {x y : A} (f : x âŸ¶ y), (idtoiso (p x)â»Â¹).hom â‰« F.map f â‰« 
       (idtoiso (p y)).hom = G.map f) -> F = G :=
begin
  intros obj_eq map_eq, fapply functor_eq,
  { exact eq_of_homotopy obj_eq },
  { fapply dep_eq_of_homotopy3, intros x y h,
    apply pathover_of_tr_eq, 
    apply @tr_fn2_of_ap_tr_ap_tr _ _ _ _ _ _ (eq_of_homotopy obj_eq) 
            (Î» bâ‚ bâ‚‚, bâ‚ âŸ¶ bâ‚‚) (F.map h) (G.map h), 
    rwr <- map_eq h, rwr ap10_eq_of_homotopy, rwr id_hom_tr_comp,
    rwr id_hom_tr_comp', rwr <- id_inv_iso_inv, rwr is_precat.assoc }
end

@[hott]
def functor_eq_obj' {A : Type _} [is_precat A] {B : Type _} [is_precat B] 
  {F G : A â¥¤ B} (p : Î  (x : A), F.obj x = G.obj x) 
    (q : Î  {x y : A} (f : x âŸ¶ y), (idtoiso (p x)â»Â¹).hom â‰« F.map f â‰« 
       (idtoiso (p y)).hom = G.map f) : 
  ap functor.obj (functor_eq' p @q) = eq_of_homotopy p :=
begin change ap _ (functor_eq _ _) = _, rwr functor_eq_obj end

end categories

end hott