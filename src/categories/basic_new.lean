import sets.algebra init2 types2 sets.axioms

universes v v' v'' v''' u u' u'' u''' w 
hott_theory

namespace hott
open hott.eq hott.sigma hott.set hott.subset hott.is_trunc hott.is_equiv

/-
We introduce precategories and categories following the HoTT book, 
Sec. 9.1. HoTT precategories have sets of homomorphisms, and HoTT categories 
prescribe univalence : Isomorphisms are equivalent to identities of objects.

As far as possible we copy the mathlib-code in [category_theory.category.default]. In particular,
we keep the distinction of universe levels for objects and morphisms of a category.
-/

namespace precategories

/-- A 'notation typeclass' on the way to defining a precategory. -/
@[hott]
class has_hom (obj : Type u) : Type (max u (v+1)) :=
  (hom : obj â†’ obj â†’ Set.{v})

infixr ` âŸ¶ `:10 := has_hom.hom  -- type as \h

/-- A preliminary structure on the way to defining a precategory,
containing the data, but none of the axioms. -/
@[hott]
class category_struct (obj : Type u) 
extends has_hom.{v} obj : Type (max u (v+1)) :=
(id       : Î  a : obj, hom a a)
(comp     : Î  {a b c : obj}, (a âŸ¶ b) â†’ (b âŸ¶ c) â†’ (a âŸ¶ c))

notation `ğŸ™` := category_struct.id -- type as \b1
infixr ` â‰« `:80 := category_struct.comp -- type as \gg

/-- The structure of a precategory. -/
@[hott, class]
structure is_precat (obj : Type u) 
extends category_struct.{v} obj : Type (max u (v+1)) :=
(id_comp : âˆ€ {a b : obj} (f : hom a b), ğŸ™ a â‰« f = f)
(comp_id : âˆ€ {a b : obj} (f : hom a b), f â‰« ğŸ™ b = f)
(assoc   : âˆ€ {a b c d : obj} (f : hom a b) (g : hom b c) (h : hom c d),
  (f â‰« g) â‰« h = f â‰« (g â‰« h))

attribute [hsimp] is_precat.id_comp is_precat.comp_id is_precat.assoc

@[hott]
structure Precategory :=
  (obj : Type u)
  (struct : is_precat obj)

@[hott] instance : has_coe_to_sort Precategory := 
  has_coe_to_sort.mk Type.{u} Precategory.obj

attribute [instance] Precategory.struct

end precategories

open precategories
namespace categories

/- Definition of categorical isomorphisms. -/
@[hott]
structure iso {C : Type u} [is_precat.{v} C] (a b : C) :=
  (hom : a âŸ¶ b)
  (inv : b âŸ¶ a) 
  (r_inv : inv â‰« hom = ğŸ™ b) 
  (l_inv : hom â‰« inv = ğŸ™ a) 

postfix `â»Â¹Ê°`:std.prec.max_plus := iso.inv

infix ` â‰… `:25 := iso

@[hott]
structure is_iso {C : Precategory} {a b : C} (f : a âŸ¶ b) :=
  (inv : b âŸ¶ a)
  (r_inv : inv â‰« f = ğŸ™ b)
  (l_inv : f â‰« inv = ğŸ™ a)

@[hott]
def is_iso_to_iso {C : Precategory} {a b : C} (f : a âŸ¶ b) 
  (H : is_iso f) : a â‰… b := iso.mk f H.inv H.r_inv H.l_inv

@[hott]
def iso_to_is_iso {C : Precategory} {a b : C} (f : a â‰… b) : 
  is_iso f.hom := is_iso.mk f.inv f.r_inv f.l_inv  

@[hott]
def iso.eta {C : Precategory} {a b : C} (i : a â‰… b) : 
  i = iso.mk i.hom i.inv i.r_inv i.l_inv :=
begin hinduction i, hsimp end  

@[hott, hsimp]
def inv_iso {C : Precategory} {a b : C} (i : a â‰… b) : b â‰… a :=
  iso.mk i.inv i.hom i.l_inv i.r_inv

/- Calculation rules for isomorphisms. -/
@[hott, hsimp]
def iso_inv_inv {C : Precategory} {a b : C} (i : a â‰… b) :
  (inv_iso i)â»Â¹Ê° = i.hom :=
by hsimp 

@[hott, hsimp]
def iso_rcancel {C : Precategory} {a b c : C} (i : a â‰… b)
  {g h : c âŸ¶ a} : g â‰« i.hom = h â‰« i.hom -> g = h :=
assume pc, 
have pc_inv : (g â‰« i.hom) â‰« i.inv = (h â‰« i.hom) â‰« i.inv, from 
  ap (Î» h : c âŸ¶ b, h â‰« i.inv) pc,
calc   g = g â‰« ğŸ™ a : by hsimp
     ... = g â‰« (i.hom â‰« i.inv) : by rwr <-i.l_inv
     ... = (g â‰« i.hom) â‰« i.inv : by hsimp
     ... = (h â‰« i.hom) â‰« i.inv : by rwr pc_inv
     ... = h â‰« (i.hom â‰« i.inv) : by hsimp
     ... = h â‰« ğŸ™ a : by rwr i.l_inv     
     ... = h : by hsimp 

@[hott, hsimp]
def iso_lcancel {C : Precategory} {a b c : C} (i : a â‰… b)
  {g h : b âŸ¶ c} : i.hom â‰« g = i.hom â‰« h -> g = h :=
assume cp, 
have cp_inv : i.inv â‰« (i.hom â‰« g) = i.inv â‰« (i.hom â‰« h), from 
  ap (Î» h : a âŸ¶ c, i.inv â‰« h) cp,
calc   g = ğŸ™ b â‰« g : by hsimp
     ... = (i.inv â‰« i.hom) â‰« g : by rwr <-i.r_inv
     ... = i.inv â‰« (i.hom â‰« g) : by hsimp
     ... = i.inv â‰« (i.hom â‰« h) : by rwr cp_inv
     ... = (i.inv â‰« i.hom) â‰« h : by hsimp
     ... = ğŸ™ b â‰« h : by rwr i.r_inv     
     ... = h : by hsimp 

@[hott, hsimp]
def iso_move_lr {C : Precategory} {a b c : C} (i : a â‰… b)
  (g : b âŸ¶ c) (h : a âŸ¶ c) : i.hom â‰« g = h -> g = i.inv â‰« h :=
assume pcr,
have i.inv â‰« i.hom â‰« g = i.inv â‰« h, from ap (Î» h : a âŸ¶ c, i.inv â‰« h) pcr,
calc g   = ğŸ™ b â‰« g : by hsimp
     ... = (i.inv â‰« i.hom) â‰« g : by rwr <-i.r_inv
     ... = i.inv â‰« (i.hom â‰« g) : by hsimp
     ... = i.inv â‰« h : by rwr pcr   

@[hott, hsimp]
def iso_move_rl {C : Precategory} {a b c : C} (i : a â‰… b)
  (g : c âŸ¶ a) (h : c âŸ¶ b) : g â‰« i.hom = h -> g = h â‰« i.inv :=
assume pcl,
have (g â‰« i.hom) â‰« i.inv = h â‰« i.inv, from ap (Î» h : c âŸ¶ b, h â‰« i.inv) pcl,
calc g   = g â‰« ğŸ™ a : by hsimp
     ... = g â‰« (i.hom â‰« i.inv) : by rwr <-i.l_inv
     ... = (g â‰« i.hom) â‰« i.inv : by hsimp
     ... = h â‰« i.inv : by rwr pcl     

/- Isomorphisms are uniquely determined by their underlying homomorphism:
   The inverse map by functorial equalities, and the functorial equalities 
   because the types of homomorphisms are sets. -/
@[hott]
def hom_eq_to_iso_eq {C : Precategory} {a b : C} {i j : a â‰… b} :
  i.hom = j.hom -> i = j :=
assume hom_eq, 
have inv_eq : i.inv = j.inv, from 
  calc i.inv = i.inv â‰« ğŸ™ a : by hsimp
       ...   = i.inv â‰« (j.hom â‰« j.inv) : by rwr j.l_invâ»Â¹ 
       ...   = (i.inv â‰« j.hom) â‰« j.inv : by hsimp
       ...   = (i.inv â‰« i.hom) â‰« j.inv : by rwr hom_eqâ»Â¹
       ...   = ğŸ™ b â‰« j.inv : by rwr i.r_inv
       ...   = j.inv : by hsimp,
let R := Î» (f : a âŸ¶ b) (g : b âŸ¶ a), g â‰« f = ğŸ™ b,
    L := Î» (f : a âŸ¶ b) (g : b âŸ¶ a), f â‰« g = ğŸ™ a in
have r_inv_eq : i.r_inv =[ap011 R hom_eq inv_eq; id] j.r_inv, from 
  begin apply pathover_of_tr_eq, apply is_set.elim end,
have l_inv_eq : i.l_inv =[ap011 L hom_eq inv_eq; id] j.l_inv, from 
  begin apply pathover_of_tr_eq, apply is_set.elim end, 
calc   i = iso.mk i.hom i.inv i.r_inv i.l_inv : iso.eta i 
     ... = iso.mk j.hom j.inv j.r_inv j.l_inv : 
                                        ap0111 iso.mk hom_eq inv_eq r_inv_eq l_inv_eq
     ... = j : (iso.eta j)â»Â¹

@[hott, hsimp]
def id_is_iso {C : Type u} [is_precat.{v} C] (a : C) : a â‰… a := 
  have inv_eq : ğŸ™ a â‰« ğŸ™ a = ğŸ™ a, by hsimp,
  iso.mk (ğŸ™ a) (ğŸ™ a) inv_eq inv_eq

@[hott, hsimp]
def idtoiso {C : Type u} [is_precat.{v} C] {a b : C} : (a = b) -> (a â‰… b) :=
  begin intro eq, exact eq â–¸[Î» c, a â‰… c] id_is_iso a end

/- `idtoiso` is natural. -/
@[hott, hsimp]
def idtoiso_refl_eq {C : Precategory} (a : C) : idtoiso (refl a) = id_is_iso a :=
  by hsimp

@[hott]
def id_inv_iso_inv {C : Precategory} {câ‚ câ‚‚ : C} (p : câ‚ = câ‚‚) :
  idtoiso pâ»Â¹ = inv_iso (idtoiso p) := 
begin hinduction p, refl end 

/- The next two facts correspond to [HoTT-Book, Lem.9.1.9]. -/
@[hott]
def id_hom_tr_comp {C : Precategory} {câ‚ câ‚‚ d : C} (p : câ‚ = câ‚‚)
  (h : câ‚ âŸ¶ d) : p â–¸ h = (idtoiso p)â»Â¹Ê° â‰« h :=
begin hinduction p, hsimp end   

@[hott]
def id_hom_tr_comp' {C : Precategory} {câ‚ câ‚‚ d : C} (p : câ‚ = câ‚‚)
  (h : d âŸ¶ câ‚) : p â–¸ h = h â‰« (idtoiso p).hom :=
begin hinduction p, hsimp end

/-- The structure of a category. -/
@[hott]
class is_cat (obj : Type u) extends is_precat.{v} obj :=
  (ideqviso : âˆ€ a b : obj, is_equiv (@idtoiso _ _ a b)) 

attribute [instance] is_cat.ideqviso

@[hott]
structure Category :=
  (obj : Type u)
  (struct : is_cat obj)
/-
@[hott, instance]
def Cat.to_obj : has_coe_to_sort Category := 
  has_coe_to_sort.mk Type.{u} Category.obj
-/
@[hott, instance]
def Cat.to_Precat : has_coe Category Precategory := 
  has_coe.mk (Î» C : Category, Precategory.mk C.obj (C.struct.to_is_precat))

attribute [instance] Category.struct

@[hott, hsimp]
def category.isotoid {C : Category} : 
  Î  {a b : C}, a â‰… b -> a = b :=
assume a b iso,  
@is_equiv.inv _ _ (@idtoiso C.obj _ a b) (is_cat.ideqviso a b) iso  

@[hott, hsimp]
def category.idtoiso_rinv {C : Category} {a b : C} :
  âˆ€ i : a â‰… b, idtoiso (idtoisoâ»Â¹á¶  i) = i :=
is_equiv.right_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_linv {C : Category} {a b : C} :
  âˆ€ p : a = b, idtoisoâ»Â¹á¶  (idtoiso p) = p :=
is_equiv.left_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_rinv' {C : Category} {a b : C} :
  âˆ€ i : a â‰… b, idtoiso (category.isotoid i) = i :=
is_equiv.right_inv (@idtoiso _ _ a b) 

@[hott, hsimp]
def category.idtoiso_linv' {C : Category} {a b : C} :
  âˆ€ p : a = b, category.isotoid (idtoiso p) = p :=
is_equiv.left_inv (@idtoiso _ _ a b) 

@[hott]
def isotoid_id_refl {C : Category} :
  Î  (a : C), category.isotoid (id_is_iso a) = refl a :=
begin 
  intro a, rwr <- idtoiso_refl_eq  a, 
  exact category.idtoiso_linv (refl a) 
end 

@[hott]
def iso_hom_tr_comp {C : Category} {câ‚ câ‚‚ d : C} (i : câ‚ â‰… câ‚‚)
  (h : câ‚ âŸ¶ d) : (idtoisoâ»Â¹á¶  i) â–¸ h = iâ»Â¹Ê° â‰« h :=
begin 
  rwr <-(category.idtoiso_rinv i),  
  rwr category.idtoiso_linv (idtoisoâ»Â¹á¶  i),
  exact id_hom_tr_comp (idtoisoâ»Â¹á¶  i) h
end 

@[hott]
def iso_hom_tr_comp' {C : Category} {câ‚ câ‚‚ d : C} (i : câ‚ â‰… câ‚‚)
  (h : d âŸ¶ câ‚) : (idtoisoâ»Â¹á¶  i) â–¸ h = h â‰« i.hom :=
begin 
  rwr <-(category.idtoiso_rinv i),  
  rwr category.idtoiso_linv (idtoisoâ»Â¹á¶  i),
  exact id_hom_tr_comp' (idtoisoâ»Â¹á¶  i) h
end

end categories

end hott