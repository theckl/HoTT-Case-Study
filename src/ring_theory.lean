import hott.algebra.ring set_theory categorial_limits pathover2

universes u v w
hott_theory

namespace hott
open is_trunc hott.is_equiv hott.algebra hott.set category_theory 

namespace algebra

/- Bundled structure of commutative rings -/
@[hott] 
structure CommRing :=
(carrier : Set) (struct : comm_ring carrier)

@[hott]
instance CommRing_to_Set : has_coe CommRing.{u} Set.{u} :=
  âŸ¨CommRing.carrierâŸ©

@[hott]
instance CommRing_to_Type : has_coe_to_sort CommRing.{u} :=
  has_coe_to_sort.mk (Type u) (Î» R : CommRing, â†¥R.carrier)  

attribute [instance] CommRing.struct

/- We now construct the category structure on commutative rings. 

   We first define ring homomorphisms without inheritance, as for 
   this project, we do not need homomorphisms of monoids and groups. -/
@[hott]
structure ring_hom (M N : CommRing.{u}) :=
  (to_fun : M -> N)
  (map_one : to_fun 1 = 1)
  (map_mul : âˆ€ a b : M, to_fun (a * b) = to_fun a * to_fun b)
  (map_zero : to_fun 0 = 0)
  (map_add : âˆ€ a b : M, to_fun (a + b) = to_fun a + to_fun b)

infixr ` â†’+* `:25 := ring_hom

@[hott]
instance ring_hom_to_fun (M N : CommRing) : 
  has_coe_to_fun (ring_hom M N) :=
âŸ¨Î» _, M -> N, Î» h, h.to_funâŸ©  

/- Needed for calculations. -/
@[hott]
def rh_map_one {R S : CommRing} (f : R â†’+* S) :
  f 1 = 1 :=
f.map_one   

@[hott]
def rh_map_mul {R S : CommRing} (f : R â†’+* S) :
  âˆ€ râ‚ râ‚‚ : R, f (râ‚ * râ‚‚) = f râ‚ * f râ‚‚ :=
f.map_mul  

@[hott]
def rh_map_zero {R S : CommRing} (f : R â†’+* S) :
  f 0 = 0 :=
f.map_zero   

@[hott]
def rh_map_add {R S : CommRing} (f : R â†’+* S) :
  âˆ€ râ‚ râ‚‚ : R, f (râ‚ + râ‚‚) = f râ‚ + f râ‚‚ :=
f.map_add

/- We show the HoTTism that all the ring homomrphisms between two
   commutative rings by showing that ring homomorphisms are completely
   determined by the map between the underlying sets. This means that the
   forgetful functor from rings to sets is faithful. 
   
   Most of the proofs use that the structure equations are propositions
   because the source and target of the homomorphisms are sets. -/
@[hott]
def ring_hom_eta {M N : CommRing} (f : M â†’+* N) : 
  f = ring_hom.mk f.to_fun f.map_one f.map_mul f.map_zero f.map_add :=
begin hinduction f, refl end

@[hott, hsimp]
def rh_fun_eq {M N : CommRing} {f g : M â†’+* N} (p : f = g) : 
  f.to_fun = g.to_fun :=
ap ring_hom.to_fun p 

@[hott, hsimp, reducible]
def rh_fun_one_eq {M N : CommRing} {f g : M â†’+* N} (p : â‡‘f = g) :
  f.map_one =[p; Î» h : M -> N, h 1 = 1] g.map_one :=
begin apply pathover_of_tr_eq, exact is_prop.elim _ _ end  

@[hott, hsimp]
def rh_fun_mul_eq {M N : CommRing} {f g : M â†’+* N} (p : â‡‘f = g) :
  f.map_mul 
    =[p; Î» h : M -> N, âˆ€ a b : M, h (a * b) = h a * h b] g.map_mul :=
begin apply pathover_of_tr_eq, exact is_prop.elim _ _ end    

@[hott, hsimp]
def rh_fun_zero_eq {M N : CommRing} {f g : M â†’+* N} (p : â‡‘f = g) :
  f.map_zero =[p; Î» h : M -> N, h 0 = 0] g.map_zero :=
begin apply pathover_of_tr_eq, exact is_prop.elim _ _ end  

@[hott, hsimp]
def rh_fun_add_eq {M N : CommRing} {f g : M â†’+* N} (p : â‡‘f = g) :
  f.map_add 
    =[p; Î» h : M -> N, âˆ€ a b : M, h (a + b) = h a + h b] g.map_add :=
begin apply pathover_of_tr_eq, exact is_prop.elim _ _ end

@[hott, hsimp]
def rh_fun_to_hom {M N : CommRing} {f g : M â†’+* N} (p : â‡‘f = g) : f = g :=
  (ring_hom_eta f) â¬ 
  (apd01111 ring_hom.mk p (rh_fun_one_eq p) (rh_fun_mul_eq p) 
                          (rh_fun_zero_eq p) (rh_fun_add_eq p)) â¬ 
  (ring_hom_eta g)â»Â¹

/- I don't understand why this proof must be so long. I was not able to tell
   Lean how to process the `idp` and `refl` arguments automatically. -/
@[hott]
def rh_fun_hom_rinv {M N : CommRing} {f g : M â†’+* N} (p : f = g) :
  rh_fun_to_hom (rh_fun_eq p) = p :=
have râ‚ : rh_fun_one_eq (refl â‡‘f) = idpo, by hsimp; refl, 
have râ‚‚ : rh_fun_mul_eq (refl â‡‘f) = idpo, by hsimp; refl,
have râ‚ƒ : rh_fun_zero_eq (refl â‡‘f) = idpo, by hsimp; refl, 
have râ‚„ : rh_fun_add_eq (refl â‡‘f) = idpo, by hsimp; refl,
have q : apd01111 ring_hom.mk (refl â‡‘f) (rh_fun_one_eq (refl â‡‘f)) 
         (rh_fun_mul_eq (refl â‡‘f)) (rh_fun_zero_eq (refl â‡‘f))
         (rh_fun_add_eq (refl â‡‘f)) = idp, 
  by rwr râ‚; rwr râ‚‚; rwr râ‚ƒ; rwr râ‚„; hsimp,   
begin 
  hinduction p, change rh_fun_to_hom (refl f) = refl f,
  change (ring_hom_eta f) â¬ _ â¬ (ring_hom_eta f)â»Â¹ = idp,
  rwr q, rwr con_idp, rwr con.right_inv 
end

@[hott]
def rh_fun_hom_linv {M N : CommRing} {f g : M â†’+* N} (p : â‡‘f = g) :
  rh_fun_eq (rh_fun_to_hom p) = p :=
@is_set.elim (M -> N) _ _ _ _ _  

@[hott]
def rh_fun_eq_equiv_hom_eq {M N : CommRing} (f g : M â†’+* N) : 
  (â‡‘f = g) â‰ƒ (f = g) :=
equiv.mk rh_fun_to_hom (adjointify rh_fun_to_hom rh_fun_eq 
                                   rh_fun_hom_rinv rh_fun_hom_linv)   

@[hott, instance]
def ring_hom_is_set (M N : CommRing) : is_set (M â†’+* N) :=
  have set_eq_eq : âˆ€ (g h : M â†’+* N) (p q : g = h), p = q, from
    assume g h,
    have is_prop_fun_eq : is_prop (â‡‘g = h), from 
      is_prop.mk (@is_set.elim (M -> N) _ _ _),
    have is_prop_hom_eq : is_prop (g = h), from 
      is_trunc_equiv_closed -1 (rh_fun_eq_equiv_hom_eq g h) is_prop_fun_eq,
    @is_prop.elim _ is_prop_hom_eq, 
  is_set.mk _ set_eq_eq   

/- Next, we define the category structure on `CommRing`. -/
@[hott, instance]
def ring_has_hom : has_hom CommRing :=
  has_hom.mk (Î» R S : CommRing, Set.mk (ring_hom R S) (ring_hom_is_set R S))

@[hott, hsimp, reducible]
def id_CommRing (R : CommRing) : R âŸ¶ R :=
  let id_R := @set.id R in
  have one_R : id_R 1 = 1, by refl,
  have mul_R : âˆ€ r s : R, id_R (r * s) = (id_R r) * (id_R s), 
    by intros r s; refl,
  have zero_R : id_R 0 = 0, by refl,
  have add_R : âˆ€ r s : R, id_R (r + s) = (id_R r) + (id_R s), 
    by intros r s; refl,
  ring_hom.mk id_R one_R mul_R zero_R add_R

@[hott, hsimp, reducible]
def comp_CommRing {R S T : CommRing} (f : R â†’+* S) (g : S â†’+* T) : R â†’+* T :=
  let h := Î» r : R, g (f r) in
  have h_one : h 1 = 1, from
    calc h 1 = g (f 1) : rfl
         ... = g 1 : by rwr rh_map_one
         ... = 1 : by rwr rh_map_one,
  have h_mul : âˆ€ râ‚ râ‚‚ : R, h (râ‚ * râ‚‚) = h râ‚ * h râ‚‚, from assume râ‚ râ‚‚, 
    calc h (râ‚ * râ‚‚) = g (f (râ‚ * râ‚‚)) : rfl
         ... = g (f râ‚ * f râ‚‚) : by rwr rh_map_mul
         ... = g (f râ‚) * g (f râ‚‚) : by rwr rh_map_mul
         ... = h râ‚ * h râ‚‚ : rfl,
  have h_zero : h 0 = 0, from 
    calc h 0 = g (f 0) : rfl
         ... = g 0 : by rwr rh_map_zero
         ... = 0 : by rwr rh_map_zero,
  have h_add : âˆ€ râ‚ râ‚‚ : R, h (râ‚ + râ‚‚) = h râ‚ + h râ‚‚, from assume râ‚ râ‚‚, 
    calc h (râ‚ + râ‚‚) = g (f (râ‚ + râ‚‚)) : rfl
         ... = g (f râ‚ + f râ‚‚) : by rwr rh_map_add
         ... = g (f râ‚) + g (f râ‚‚) : by rwr rh_map_add
         ... = h râ‚ + h râ‚‚ : rfl,
  ring_hom.mk h h_one h_mul h_zero h_add

@[hott, instance]
def CommRing_cat_struct : category_struct CommRing :=
  category_struct.mk id_CommRing @comp_CommRing

/- The equalities of homomorphisms making up a precategory follow from 
   the equalities of the maps on the underlying sets. -/
@[hott, hsimp]
def id_comp_CommRing {R S : CommRing.{u}} : Î  (f : R âŸ¶ S), 
  ðŸ™ R â‰« f = f :=
assume f,  
have hom_eq : âˆ€ r : R, (ðŸ™ R â‰« f) r = f r, from assume r, rfl,
have fun_eq : â‡‘(ðŸ™ R â‰« f) = f, from eq_of_homotopy hom_eq,   
rh_fun_to_hom fun_eq  

@[hott, hsimp]
def comp_id_CommRing {R S : CommRing.{u}} : Î  (f : R âŸ¶ S), 
  f â‰« ðŸ™ S = f :=
assume f,  
have hom_eq : âˆ€ r : R, (f â‰« ðŸ™ S) r = f r, from assume r, rfl,
have fun_eq : â‡‘(f â‰« ðŸ™ S) = f, from eq_of_homotopy hom_eq,   
rh_fun_to_hom fun_eq

@[hott, hsimp]
def assoc_CommRing {R S T U : CommRing.{u}} : 
  Î  (f : R âŸ¶ S) (g : S âŸ¶ T) (h : T âŸ¶ U), (f â‰« g) â‰« h = f â‰« (g â‰« h) :=
assume f g h,
have hom_eq : âˆ€ r : R, ((f â‰« g) â‰« h) r = (f â‰« (g â‰« h)) r, from assume r, rfl,
have fun_eq : â‡‘((f â‰« g) â‰« h) = f â‰« (g â‰« h), from eq_of_homotopy hom_eq, 
rh_fun_to_hom fun_eq 

@[hott, instance]
def CommRing_precategory : precategory CommRing.{u} :=
  precategory.mk @id_comp_CommRing @comp_id_CommRing @assoc_CommRing

/- We now show that `CommRing` is a category. 

   To this purpose we use univalence to construct an equality of rings
  from an isomorphy. This is noncomputable. -/
@[hott]
def CommRing_isotoSetid (R S : CommRing) : R â‰… S -> R.carrier = S.carrier :=
  assume isoRS,
  let h := â‡‘isoRS.hom, i := â‡‘isoRS.inv in
  have r_inv : âˆ€ s : S, h (i s) = s, from assume s, 
    calc h (i s) = (isoRS.inv â‰« isoRS.hom) s : by refl 
         ... = (id_CommRing S) s : by rwr isoRS.r_inv
         ... = s : by refl,
  have l_inv : âˆ€ r : R, i (h r) = r, from assume r, 
    calc i (h r) = (isoRS.hom â‰« isoRS.inv) r : by refl 
         ... = (id_CommRing R) r : by rwr isoRS.l_inv
         ... = r : by refl, 
  have equivRS : R â‰ƒ S, from equiv.mk h (adjointify h i r_inv l_inv),             
  have eqTypeRS : â†¥R = â†¥S, from ua equivRS, 
  set_eq_equiv_car_eq.symm.to_fun eqTypeRS

#check comm_ring.mk

end algebra

end hott
