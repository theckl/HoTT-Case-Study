import sets.basic 

universes u v w
hott_theory

namespace hott
open hott.set hott.is_trunc hott.is_equiv hott.eq hott.trunc hott.sigma 

namespace subset

set_option pp.universes false

/- We define subsets of sets [A] by predicates on `A`, with a corecion to the set of all 
   elements of `A` satisfying the predicate, and a coercion of the objects of this set to
   the elements of `A`. -/

@[hott]
def Subset (A : Set) := A -> trunctype.{0} -1
   
@[hott]
instance (A : Set.{u}) : has_coe_to_sort (Subset A) := 
  has_coe_to_sort.mk Type.{u} (Œª B, Œ£ a : A, B a)

/- A HoTT-ism, but also a way to produce sets. -/
@[hott, instance]
def is_set_pred {A : Set} : Œ† (B : Subset A), is_set (Œ£ (a : A), B a) :=
  assume B, 
  have forall (a : A), is_set (B a), from 
    assume a, 
    have is_prop (B a), from trunctype.struct (B a),
    is_trunc_succ (B a) -1, 
  is_trunc_sigma (Œª a : A, B a) 0 

@[hott]
def pred_Set {A : Set} (B : Subset A) : Set :=
  Set.mk (Œ£ (a : A), B a) (is_set_pred B)  

@[hott] 
def pred_Set_map {A : Set} (B : Subset A) : pred_Set B -> A := Œª b, b.1  

@[hott]
def pred_Set_map_is_inj {A : Set} (B : Subset A) : 
  is_set_injective (pred_Set_map B) :=
begin 
  intros b‚ÇÅ b‚ÇÇ H, fapply sigma_eq, 
  { exact H }, 
  { apply pathover_of_tr_eq, exact is_prop.elim _ _ } 
end

@[hott]
instance {A : Set} (B : Subset A) : has_coe ‚Ü•B ‚Ü•A :=
  ‚ü®Œª b, b.1‚ü©

/- An equivalent, but less useful way to define subsets. -/
@[hott]
structure inj_Subset (A : Set.{u}) :=
   (carrier : Set.{u}) (map : carrier -> A)  (inj : is_set_injective map) 

@[hott]
def pred_to_inj_sset {A : Set} : Subset A -> inj_Subset A :=
  assume B,
  inj_Subset.mk (pred_Set B) (pred_Set_map B) (pred_Set_map_is_inj B) 

@[hott]
def inj_to_pred_sset {A : Set} : inj_Subset A -> Subset A :=
  assume B_inj,
  assume a, prop_resize (image B_inj.map a) 

@[hott]
def inj_car_eqv_pred_sset {A : Set.{u}} {B_car : Set.{u}} (map : B_car -> A) 
  (inj : is_set_injective map) : pred_Set (Œª a : A, prop_resize.{0 u} (image map a)) ‚âÉ B_car :=
let HP : (Œ£ a : A, prop_resize.{0 u} (image map a)) -> (Œ£ a : A, fiber map a) := 
  Œª a_im, 
  ‚ü®a_im.1, @untrunc_of_is_trunc _ _ (set_inj_implies_unique_fib map inj a_im.1) 
             (prop_resize_to_prop a_im.2)‚ü© in
have H : ‚àÄ a_im : (Œ£ a : A, prop_resize.{0 u} (image map a)), map (HP a_im).2.1 = a_im.1, from
  assume a_im, (HP a_im).2.2,             
let f : (Œ£ a : A, prop_resize.{0 u} (image map a)) -> B_car := Œª a_im, (HP a_im).2.1 in
let g : B_car -> (Œ£ a : A, prop_resize.{0 u} (image map a)) := 
                                 Œª b, ‚ü®map b, prop_to_prop_resize (tr ‚ü®b, idp‚ü©)‚ü© in  
have rinv : ‚àÄ b : B_car, f (g b) = b, from 
  assume b,
  calc f (g b) = (HP (g b)).2.1 : rfl
       ... = b : ap fiber.point (@is_prop.elim _ (set_inj_implies_unique_fib map inj (map b))
                                 (fiber.mk (HP (g b)).2.1 (H (g b))) (fiber.mk b idp)),
have linv : ‚àÄ (a_im : Œ£ a : A, prop_resize.{0 u} (image map a)), g (f a_im) = a_im, from 
  begin 
    intro a_im, fapply sigma_eq,
    { calc (g (f a_im)).1 = map (f a_im) : rfl
           ... = a_im.1 : H a_im },
    { apply pathover_of_tr_eq, exact is_prop.elim _ _ }
  end,
equiv.mk f (adjointify f g rinv linv) 

@[hott]
def pred_sset_linv {A : Set} (B_inj : inj_Subset A) : 
  pred_to_inj_sset (inj_to_pred_sset B_inj) = B_inj :=
begin
  hinduction B_inj,
  change inj_Subset.mk (pred_Set (Œª a : A, prop_resize (image map a))) (Œª b, b.1) _ =
         inj_Subset.mk carrier map inj,
  fapply apd0111 inj_Subset.mk,
  { apply car_eq_to_set_eq,
    exact ua (inj_car_eqv_pred_sset map inj) },
  { apply pathover_of_tr_eq, apply eq_of_homotopy, intro b,
    let H : ‚àÄ A‚ÇÅ A‚ÇÇ : Set, is_equiv (@ap _ _ trunctype.carrier A‚ÇÅ A‚ÇÇ) := 
      assume A‚ÇÅ A‚ÇÇ, (@set_eq_equiv_car_eq A‚ÇÅ A‚ÇÇ).to_is_equiv,
    have H' : @car_eq_to_set_eq (pred_Set (Œª (a : ‚Ü•A), prop_resize (image map a))) carrier = 
              @is_equiv.inv _ _ _ (H _ carrier), from rfl,
    rwr H',          
    calc _ = (ua (inj_car_eqv_pred_sset map inj) ‚ñ∏[Œª C : Type _, C -> A.carrier]
                 (Œª (b : ‚Ü•(pred_Set (Œª (a : ‚Ü•A), prop_resize (image map a)))), b.fst)) b : 
               ap_inv_po_fn (Œª (b : (pred_Set (Œª (a : ‚Ü•A), 
                                prop_resize (image map a))).carrier), b.fst) H b
                                (ua (inj_car_eqv_pred_sset map inj)) 
         ... = (Œª (b : (pred_Set (Œª (a : ‚Ü•A), prop_resize (image map a))).carrier), b.fst)
                 ((ua (inj_car_eqv_pred_sset map inj))‚Åª¬π ‚ñ∏[Œª C : Type _, C] b) : 
               tr_fn_eval_tr _ b
         ... = (cast (ua (inj_car_eqv_pred_sset map inj))‚Åª¬π b).fst : rfl
         ... = ((inj_car_eqv_pred_sset map inj)‚Åª¬π·∂† b).fst : by rwr cast_ua_inv
         ... = map b : rfl },
  { apply pathover_of_tr_eq, exact is_prop.elim _ _ }
end            

@[hott]
def pred_sset_rinv {A : Set} (B : Subset A) :
  inj_to_pred_sset (pred_to_inj_sset B) = B :=
begin
  apply eq_of_homotopy, intro a,
  change prop_resize (image (Œª b : pred_Set B, b.1) a) = B a,
  fapply prop_iff_eq,
  { intro p, 
    let a_fib : fiber (Œª (b : ‚Ü•(pred_Set B)), b.fst) a := 
      @untrunc_of_is_trunc _ _ (set_inj_implies_unique_fib (Œª (b : ‚Ü•(pred_Set B)), b.fst) 
        (pred_to_inj_sset B).inj a) (prop_resize_to_prop p),
    have H : a = a_fib.1.1, by from (a_fib.2)‚Åª¬π,
    rwr H, exact a_fib.1.2 },
  { intro p, apply prop_to_prop_resize, apply tr, exact ‚ü®‚ü®a,p‚ü©, idp‚ü© }
end  

@[hott]
def pred_eqv_inj_sset {A : Set} : (Subset A) ‚âÉ (inj_Subset A) :=
  equiv.mk pred_to_inj_sset (adjointify pred_to_inj_sset inj_to_pred_sset
                                        pred_sset_linv pred_sset_rinv) 

/- On subsets we can introduce the `‚àà`-notation and the `‚äÜ`-notation of set theory. -/
@[hott]
protected def elem {A : Set} (a : A) (S : Subset A) :=
  S a

@[hott]
protected def not_elem {A : Set} (a : A) (S : Subset A) :=
  Not (S a)

hott_theory_cmd "local infix  `‚àà`:70 := hott.subset.elem"
hott_theory_cmd "local infix  `‚àâ`:70 := hott.subset.not_elem" 

notation `{ ` binder ` ‚àà ` B ` | ` P:scoped  ` }` := (P : Subset B)
notation `{ ` binder ` ‚àà ` B ` | ` P:scoped  ` }` := (P : Subset (to_Set B)) 

@[hott, reducible]
def pred_elem {A : Set} {P : Subset A} (a : A) : a ‚àà { a ‚àà A | P a } <-> P a :=
  have imp‚ÇÅ : a ‚àà { a ‚àà A | P a } -> P a, from
    begin intro elem_a_P, exact elem_a_P end,
  have imp‚ÇÇ : P a -> a ‚àà { a ‚àà A | P a }, from
    begin intro pred_a, exact pred_a end,  
  ‚ü®imp‚ÇÅ, imp‚ÇÇ‚ü© 

@[hott]
def elem_to_pred {A : Set} {P : Subset A} (a : A) : a ‚àà { a ‚àà A | P a } -> P a :=
begin intro elem_a_P, exact elem_a_P end

@[hott]
def pred_to_elem {A : Set} {P : Subset A} (a : A) : P a -> a ‚àà { a ‚àà A | P a } :=
begin intro pred_a, exact pred_a end 

@[hott]   
inductive construct_elem {A : Set} (P : A ‚Üí trunctype -1) 
| intro (w : A) (h : P w) : construct_elem

attribute [intro] construct_elem.intro

@[hott]
def exists_elem {A : Set} (P : A ‚Üí Prop) : Prop :=
  ‚à• construct_elem P ‚à• 

notation `‚àÉ` binder `‚àà` B `,` P:scoped := @exists_elem B P 

@[hott]
def unique_elem {A : Set} (P : A ‚Üí Prop) := (Œ£ a : A, P a) √ó is_prop (Œ£ a : A, P a)

notation `‚àÉ!` binder `‚àà` B `,` P:scoped := @unique_elem B P 

@[hott]
def unique_to_elem {A : Set} (P : A ‚Üí Prop) (ue : unique_elem P) : A := ue.1.1

@[hott]
def unique_to_pred {A : Set} (P : A ‚Üí Prop) (ue : unique_elem P) : P (unique_to_elem P ue) :=
  ue.1.2

@[hott]
def unique_to_uniq {A : Set} (P : A ‚Üí Prop) (ue : unique_elem P) : is_prop (Œ£ a : A, P a) :=
  ue.2

@[hott]
def is_subset_of {A : Set} (B C : Subset A) :=
  forall a : A, a ‚àà B -> a ‚àà C

@[hott, instance]
def is_prop_subset {A : Set} (B C : Subset A) : is_prop (is_subset_of B C) :=
  have Pss : ‚àÄ a : A, is_prop (a ‚àà B -> a ‚àà C), from 
    assume a, is_prop_map ((a ‚àà C).struct),
  is_prop_dprod Pss

@[hott]
def is_Subset_of {A : Set} (B C : Subset A) : trunctype -1 :=
  Prop.mk (is_subset_of B C) (is_prop_subset B C)

hott_theory_cmd "local infix `‚äÜ`:50 := hott.subset.is_Subset_of"

/- We show some basic facts on subsets. 

   Two subsets are equal iff they are subsets of each other. -/
@[hott]
def sset_eq_iff_inclusion {A : Set} (B C : Subset A) :
  B = C ‚Üî (B ‚äÜ C) √ó (C ‚äÜ B) :=
have imp1 : B = C -> (B ‚äÜ C) √ó (C ‚äÜ B), from 
  assume sset_eq, 
  have incl1 : B ‚äÜ C, from assume a a_in_B,
    sset_eq ‚ñ∏[Œª D : Subset A, ‚Ü•(a ‚àà D)] a_in_B,
  have incl2 : C ‚äÜ B, from assume a a_in_C,
    sset_eq‚Åª¬π ‚ñ∏[Œª D : Subset A, ‚Ü•(a ‚àà D)] a_in_C, 
  prod.mk incl1 incl2, 
have imp2 : (B ‚äÜ C) √ó (C ‚äÜ B) -> B = C, from 
  assume incl, let incl1 := prod.fst incl, incl2 := prod.snd incl in 
  begin 
    apply eq_of_homotopy, intro a, fapply prop_iff_eq, 
    { exact incl1 a },
    { exact incl2 a } 
  end,
prod.mk imp1 imp2

/- A criterion for not being a subset -/
@[hott]
def not_ss_elem {A : Set} (B‚ÇÅ B‚ÇÇ : Subset A) : 
  Not (B‚ÇÅ ‚äÜ B‚ÇÇ) <-> ‚àÉ a ‚àà A, a ‚àà B‚ÇÅ and Not (a ‚àà B‚ÇÇ) :=
begin 
  apply pair,
  { intro not_ss, 
    have ex_not_imp : ‚Ü•‚à•Œ£ a : A, Not (to_Prop (a ‚àà B‚ÇÅ -> a ‚àà B‚ÇÇ))‚à•, from 
      (not_all _).1 not_ss, 
    hinduction ex_not_imp with eni, 
    apply tr, fapply construct_elem.intro, 
    { exact eni.1 },
    { exact (neg_imp _ _).1 eni.2 } },
  { intros ex Bss, hinduction ex with cons, hinduction cons with a' Pa, 
    exact (neg_imp _ _).2 Pa (Bss a') }
end

/- Two subsets that always exist. -/
@[hott]
def total_Subset (A : Set) : Subset A := 
  Œª a, True

@[hott]
def all_elem {A : Set} (a : A) : a ‚àà total_Subset A :=
  true.intro

@[hott]
def total_pred_Set_eq_Set (A : Set) : pred_Set (total_Subset A) = A :=
begin 
  apply bij_to_set_eq, fapply has_inverse_to_bijection, 
  { intro a_pred, exact a_pred.1 },
  { intro a, exact ‚ü®a, true.intro‚ü© },
  { fapply is_set_inverse_of.mk, 
    { intro a, refl },
    { intro a_pred, fapply sigma_eq,
      { refl },
      { apply pathover_of_tr_eq, exact is_prop.elim _ _ } } }
end     

@[hott]   
def empty_Subset (A : Set) : Subset A :=
  Œª a, to_Prop empty

@[hott]
def empty_not_elem {A : Set} (a : A) : a ‚àâ empty_Subset A :=
begin intro el_a, hinduction el_a end  

/- The image subset (of a subset) under a map between sets. -/
@[hott]
def ss_Image {A B : Set} (f : A -> B) (C : Subset A): Subset B := 
  Œª b : B, prop_resize (image (f ‚àò (pred_Set_map C)) b) 

@[hott]
def Image {A B : Set} (f : A -> B) : Subset B :=
  ss_Image f (total_Subset A)

/- Some lemmas to exploit the image of a subset under a map. -/
@[hott]
def ss_image_preimage {A B : Set} (f : A -> B) (C : Subset A) : 
  ‚àÄ b : B, b ‚àà ss_Image f C -> image (f ‚àò (pred_Set_map C)) b :=
begin intros b el, hinduction (prop_resize_to_prop el) with eq fa, exact tr fa end   

@[hott]
def ss_image_el {A : Set.{u}} {B : Set.{v}} (f : A -> B) (C : Subset A) : 
  ‚àÄ (a : A), a ‚àà C -> f a ‚àà ss_Image f C :=
begin 
  intros a ela, apply prop_to_prop_resize,
  apply tr, fapply fiber.mk, 
  { exact ‚ü®a, ela‚ü© },
  { exact idp } 
end

@[hott]
def ss_im_preim_el {A B : Set} (f : A -> B) (C : Subset A) (b : B) :
  b ‚àà ss_Image f C -> ‚à•Œ£ a : A, (a ‚àà C) √ó (f a = b)‚à• :=
begin 
  intro elb,
  have imb : ‚Ü•(image (f ‚àò (pred_Set_map C)) b), from ss_image_preimage f C b elb,
  hinduction imb with fibb,
  exact tr ‚ü®(pred_Set_map C) fibb.1, ‚ü®fibb.1.2, fibb.2‚ü©‚ü©
end 

@[hott]
def ss_im_comp {A B C : Set} (f : A -> B) (g : B -> C) (D : Subset A) :
  ‚àÄ c : C, c ‚àà ss_Image g (ss_Image f D) <-> c ‚àà ss_Image (g ‚àò f) D :=
begin 
  intro c, apply pair,
  { intro elc, 
    let imc := ss_image_preimage g (ss_Image f D) c elc,
    hinduction imc with fibc,
    let b := (pred_Set_map (ss_Image f D)) fibc.1,
    have elb : ‚Ü•(b ‚àà ss_Image f D), from fibc.1.2,
    let ima := ss_im_preim_el f D b elb,
    hinduction ima with H, let a := H.1,
    have p : c = g (f a), by rwr H.2.2; rwr <- fibc.2, 
    rwr p, exact ss_image_el (g ‚àò f) D a H.2.1 },
  { intro elc, 
    let ima := ss_im_preim_el (g ‚àò f) D c elc,
    hinduction ima with H, let a := H.1, let b := f a,
    rwr <- H.2.2, change ‚Ü•((g b)‚ààss_Image g (ss_Image f D)),
    exact ss_image_el g (ss_Image f D) b (ss_image_el f D a H.2.1) }
end  

/- The set of all subsets, the power set -/
@[hott, instance]
def Powerset_is_set {A : Set} : is_set (Subset A) := 
begin
  fapply is_set.mk, intros B C p q,
  have eq_eqv_hom : (B = C) ‚âÉ (B ~ C), from eq_equiv_homotopy B C,
  have is_prop_hom : is_prop (B ~ C), from 
    have pP : forall a : A, is_prop (B a = C a), from 
      assume a, is_trunc_eq -1 (B a) (C a),
    @is_prop_dprod _ (Œª a : A, B a = C a) pP,  
  have H_eqv : is_prop (B = C), from 
    is_trunc_is_equiv_closed -1 (equiv.to_fun eq_eqv_hom)‚Åª¬π·∂† is_prop_hom, 
  exact @is_prop.elim _ H_eqv p q
end  

@[hott, reducible]
def Powerset (A : Set) : Set :=
  Set.mk (Subset A) Powerset_is_set

hott_theory_cmd "local prefix `ùí´`:100 := hott.subset.Powerset"    

/- A criterion for a type to be a set: it is mapped injectively to a set. 
   The proof needs some facts that were already shown for sets in [sets.basic]. -/
/- fibers of injective maps only contain one element. -/
@[hott]
def inj_implies_unique_fib {A : Set} {B : Type _} (f : B -> A) : 
  (‚àÄ b‚ÇÅ b‚ÇÇ : B, f b‚ÇÅ = f b‚ÇÇ -> b‚ÇÅ = b‚ÇÇ) -> ‚àÄ a : A, is_prop (fiber f a) :=
assume f_inj a,
have H : forall fb1 fb2 : fiber f a, fb1 = fb2, from
  assume fb1 fb2,
  match fb1, fb2 with fiber.mk b1 e1, fiber.mk b2 e2 :=    
    have eqb : b1 = b2, from f_inj b1 b2 (e1 ‚¨ù e2‚Åª¬π), 
    have eqbeq : e1 =[eqb;(Œª b : B, f b = a)] e2, from pathover_of_tr_eq (is_set.elim _ _),
    apd011 fiber.mk eqb eqbeq 
  end,  
is_prop.mk H 

@[hott]
def inj_to_Set_is_set (A : Type _) {B : Set} {f : A -> B} 
  (inj_f : ‚àÄ a‚ÇÅ a‚ÇÇ : A, f a‚ÇÅ = f a‚ÇÇ -> a‚ÇÅ = a‚ÇÇ) : is_set A :=
begin
  let Im_f := pred_Set (Œª b : B, prop_resize (image f b)),
  have A_eqv_Im_f : A ‚âÉ Im_f, from 
  begin
    fapply equiv.MK, 
    { intro a, fapply sigma.mk, exact f a, 
      apply prop_to_prop_resize, apply tr, exact fiber.mk a idp },
    { intro b_im, exact (@untrunc_of_is_trunc _ _ (inj_implies_unique_fib f inj_f b_im.1) 
                                              (prop_resize_to_prop b_im.2)).1 },
    { intro b_im, fapply sigma_eq,
      { exact (@untrunc_of_is_trunc _ _ (inj_implies_unique_fib f inj_f b_im.1) 
                                                        (prop_resize_to_prop b_im.2)).2 }, 
      { apply pathover_of_tr_eq, exact is_prop.elim _ _ } },
    { intro a, change _ = (@fiber.mk _ _ f (f a) a idp).point, apply ap fiber.point, 
      exact @is_prop.elim _ (inj_implies_unique_fib f inj_f (f a)) _ _ }
  end,
  apply is_trunc_equiv_closed_rev, apply A_eqv_Im_f, apply_instance
end  

end subset

end hott